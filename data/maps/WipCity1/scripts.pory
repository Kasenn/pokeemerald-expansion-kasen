mapscripts WipCity1_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: WipCity1_Visited
}

script WipCity1_Visited{
    setflag(FLAG_VISITED_WIP_CITY1)
    end
}

script AldeleafCity_BirdTrainer{
    msgbox(AldeleafCity_BirdTrainer_Text, MSGBOX_NPC)
    end
}

text AldeleafCity_BirdTrainer_Text{
    "I wanted to take on the gym but I don't\n"
    "think my bird Pokémon stand a chance…"
}

script Aldeleaf_Sudowoodo{
    lockall
	goto_if_unset(FLAG_BADGE02_GET, Aldeleaf_Sudowoodo_NoCut)
	checkpartymove(MOVE_CUT)
	if(var(VAR_RESULT) == PARTY_SIZE){
		goto(Aldeleaf_Sudowoodo_NoCut)
	}
	else{
        setfieldeffectargument(0, VAR_RESULT)
        bufferpartymonnick(STR_VAR_1, VAR_RESULT)
        buffermovename(STR_VAR_2, MOVE_CUT)
        msgbox(Aldeleaf_WantToCut, MSGBOX_YESNO)
        goto_if_eq(VAR_RESULT, NO, EventScript_CancelCut)
        msgbox(Text_MonUsedFieldMove, MSGBOX_DEFAULT)
        closemessage
        dofieldeffect(FLDEFF_USE_CUT_ON_TREE)
        waitstate
        goto(Aldeleaf_Sudowoodo_CutDown)
        end
	}
}

text Aldeleaf_WantToCut{
	"There's a weird tree blocking\n"
    "the way here.\p"
	"Would you like to cut it?"
}
script Aldeleaf_Sudowoodo_NoCut{
	lock
	faceplayer
	waitse
	playse(SE_SUDOWOODO_SHAKE)
	applymovement(1, Aldeleaf_SudowoodoShake)
	waitmovement(1)
	release
	end
}
movement Aldeleaf_SudowoodoShake{
	face_right
	delay_8
	face_down
	delay_8
	face_left
	delay_8
	face_down
	delay_8
	face_right
	delay_8
	face_down
	delay_8
	face_left
	delay_8
	face_down
	step_end
}
script Aldeleaf_Sudowoodo_CutDown{
	waitse
	playse(SE_SUDOWOODO_SHAKE)
	applymovement(1, Aldeleaf_SudowoodoShake)
	waitmovement(1)
	msgbox(gText_Sudowoodo_Attacked, MSGBOX_DEFAULT)
	closemessage
	waitse
	playmoncry(SPECIES_SUDOWOODO, CRY_MODE_ENCOUNTER)
	delay(40)
	waitmoncry
	setwildbattle(SPECIES_SUDOWOODO, 25)
	dowildbattle
	fadescreenswapbuffers(FADE_TO_BLACK)
	removeobject(VAR_LAST_TALKED)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	setflag(FLAG_DEFEATED_SUDOWOODO)
	release
	end
}
script Aldeleaf_TrainerNearSudowoodo{
	lock
	faceplayer
    if(flag(FLAG_DEFEATED_SUDOWOODO)){
        msgbox(Aldeleaf_Text_OnceAnOddTreePastHere, MSGBOX_DEFAULT)
    }
    else{
        msgbox(Aldeleaf_OddTreeHereSeemsToWiggle, MSGBOX_DEFAULT)
    }
	release
	end
}

text Aldeleaf_Text_OnceAnOddTreePastHere{
    "Excuse me, young one.\n"
	"Have you a good memory?\p"
	"I have this feeling there once was\n"
	"an odd tree past here."
}

text Aldeleaf_OddTreeHereSeemsToWiggle{
	"Excuse me, young one.\n"
	"Have you good eyesight?\p"
	"There is an odd tree past here, and to\n"
	"my tired eyes it seems to wiggle.$"
}

