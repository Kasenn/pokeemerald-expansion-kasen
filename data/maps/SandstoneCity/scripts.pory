mapscripts SandstoneCity_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: VisitedSandstone
}

script VisitedSandstone{
    setflag(FLAG_VISITED_SANDSTONE_CITY)
    if(var(VAR_RIVAL_AROUND_SANDSTONE) == 3){
        setvar(VAR_RIVAL_AROUND_SANDSTONE, 4)
    }
    end
}

script Sandstone_Sign1{
    msgbox(Sandstone_Sign1_Text, MSGBOX_SIGN)
    end
}
script Sandstone_Sign2{
    msgbox(Sandstone_Sign2_Text, MSGBOX_SIGN)
    end
}
script Sandstone_Sign3{
    msgbox(Sandstone_Sign3_Text, MSGBOX_SIGN)
    end
}
script Sandstone_Sign4{
    msgbox(Sandstone_Sign4_Text, MSGBOX_SIGN)
    end
}
script Sandstone_Sign5{
    msgbox(Sandstone_Sign5_Text, MSGBOX_SIGN)
    end
}
script Sandstone_Sign6{
    msgbox(Sandstone_Sign6_Text, MSGBOX_SIGN)
    end
}
text Sandstone_Sign1_Text{
    "SANDSTONE CITY\n"
    "“Where Pokémon and people meet.”"
}
text Sandstone_Sign2_Text{
    "Fishing Guru's house"
}
text Sandstone_Sign3_Text{
    "Sandstone Library"
}
text Sandstone_Sign4_Text{
    "{UP_ARROW} Adamant Tower\n"
    "{DOWN_ARROW} Route 12\l"
    "{RIGHT_ARROW} Route 13\l"
    "{LEFT_ARROW} Route 14"
}
text Sandstone_Sign5_Text{
    "Sandstone City Pokémon Gym"
}
text Sandstone_Sign6_Text{
    "Sandstone Department Store"
}

script Sandstone_Boy2{
    msgbox(Sandstone_Boy2_Text, MSGBOX_NPC)
    end
}
script Sandstone_Lass{
    msgbox(Sandstone_Lass_Text, MSGBOX_NPC)
    end
}
script Sandstone_GameboyKid1{
    msgbox(Sandstone_GameboyKid1_Text, MSGBOX_NPC)
    end
}
script Sandstone_GameboyKid2{
    msgbox(Sandstone_GameboyKid2_Text, MSGBOX_NPC)
    end
}
script Sandstone_GossipWoman1{
    msgbox(Sandstone_GossipWoman1_Text, MSGBOX_NPC)
    end
}
script Sandstone_GossipWoman2{
    msgbox(Sandstone_GossipWoman2_Text, MSGBOX_NPC)
    end
}
script Sandstone_Gentleman{
    msgbox(Sandstone_Gentleman_Text, MSGBOX_NPC)
    end
}
script Sandstone_Maniac{
    if(flag(FLAG_SANDSTONE_ULTRABALL)){
        msgbox(Sandstone_Maniac_Text3, MSGBOX_NPC)
        end
    }
    lock
    faceplayer
    msgbox(Sandstone_Maniac_Text2, MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        msgbox(WhyTravel, MSGBOX_NPC)
        end
    }
    msgbox(Sandstone_Maniac_Text, MSGBOX_DEFAULT)
    checkitemspace(ITEM_ULTRA_BALL)
    if(var(VAR_RESULT) == FALSE){
        msgbox(NotEnoughRoom, MSGBOX_NPC)
        end
    }
    giveitem(ITEM_ULTRA_BALL)
    setflag(FLAG_SANDSTONE_ULTRABALL)
    release
    end
}
script Sandstone_Man3{
    msgbox(Sandstone_Man3_Text, MSGBOX_NPC)
    end
}
script Sandstone_Man5{
    msgbox(Sandstone_Man5_Text, MSGBOX_NPC)
    end
}
script Sandstone_Youngster{
    msgbox(Sandstone_Youngster_Text, MSGBOX_NPC)
    end
}
script Sandstone_Nerd{
    msgbox(Sandstone_Nerd_Text, MSGBOX_NPC)
    end
}
text Sandstone_Boy2_Text{
    "To the north of the city lies the\n"
    "Adamant Tower.\p"
    "It's a real hotspot for strong\n"
    "trainers, but in order to get in,\l"
    "you must either earn the approval\l"
    "of the Gym Leader, or go through\l"
    "rigorous training at the gym."
}
text Sandstone_Lass_Text{
    "I just returned from a trip to Unova.\p"
    "You wouldn't believe some of the\n"
    "crazy Pokémon I saw there!"
}
text Sandstone_GameboyKid1_Text{
    "We just tried trading two Pokémon that\n"
    "were meant to evolve through trading,\l"
    "but they didn't evolve at all!"
}
text Sandstone_GameboyKid2_Text{
    "Maybe the evolution method has\n"
    "changed…"
}
text Sandstone_GossipWoman1_Text{
    "The Department Store sells everything\n"
    "you could ever ask for!"
}
text Sandstone_GossipWoman2_Text{
    "Sometimes I take my Pokémon shopping\n"
    "with me.\p"
    "Seeing them be amazed by all the\n"
    "different products would put a smile\l"
    "on anyone's face!"
}
text Sandstone_Gentleman_Text{
    "As far as I can remember, Sandstone\n"
    "has always been a massive city.\p"
    "I wonder what it used to look like\n"
    "before it got this big."
}
text Sandstone_Maniac_Text2{
    "Do you like Pokémon?"
}
text Sandstone_Maniac_Text{
    "Yes, yes, you have Pokémon with you,\n"
    "so I'm sure you do!\p"
    "If you want to have even more Pokémon\n"
    "with you, you need to use one of these!"
}
text Sandstone_Maniac_Text3{
    "Take care now!"
}
text WhyTravel{
    "That so? Then why are you on a journey\n"
    "with Pokémon?"
}
text Sandstone_Man3_Text{
    "I still haven't defeated the Gym\n"
    "Leader here…\p"
    "But that's all right.\p"
    "I'm going to become such a strong\n"
    "trainer, she'll be the one who comes\l"
    "looking for ME!"
}
text Sandstone_Man5_Text{
    "I just bought a hundred Potions from\n"
    "the Department Store.\p"
    "There's no way I'll lose against\n"
    "the Gym Leader now!"
}
text Sandstone_Youngster_Text{
    "I like shorts! They're comfy\n"
    "and easy to wear!"
}
text Sandstone_Nerd_Text{
    "Want to become smart like me?\p"
    "Then you need to head to the Sandstone\n"
    "Library and read a few books!"
}

script SandstoneRival{
    if(var(VAR_RIVAL_AROUND_SANDSTONE) == 4){
        release
        end
    }
    if(var(VAR_PEARLWOOD_TOWN_STATE) == 10){
        release
        end
    }
    setflag(FLAG_MAY_COSTUME_CHANGE)
    setflag(FLAG_BRENDAN_COSTUME_CHANGE)
    call(Common_EventScript_SetupRivalGfxId)
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
    opendoor(51, 22)
    waitdooranim
    addobject(13)
    applymovement(OBJ_EVENT_ID_PLAYER, Sandstone_PushPlayerDown)
    applymovement(13, Sandstone_RivalExitStore)
    waitmovement(0)
    closedoor(51, 22)
    waitdooranim
    checkplayergender
    copyvar(VAR_TEMP_2, VAR_RESULT)
    if(var(VAR_TEMP_2) == MALE){
        msgbox(RivalMayLikeOutfit, MSGBOX_YESNO)
        if(var(VAR_RESULT) == FALSE){
            if(flag(FLAG_PC_CHANGE_COSTUME)){
                msgbox(DontLikeMaysOutfitV2)
            }
            else{
                msgbox(DontLikeMaysOutfit)
            }
            clearflag(FLAG_MAY_COSTUME_CHANGE)
            clearflag(FLAG_BRENDAN_COSTUME_CHANGE)
            if(var(VAR_RIVAL_AROUND_SANDSTONE) == 0){
                setvar(VAR_RIVAL_AROUND_SANDSTONE, 2)
            }
        }
        else{
            if(flag(FLAG_PC_CHANGE_COSTUME)){
                msgbox(LikeMaysOutfitV2)
            }
            else{
                msgbox(LikeMaysOutfit)
            }
            if(var(VAR_RIVAL_AROUND_SANDSTONE) == 0){
                setvar(VAR_RIVAL_AROUND_SANDSTONE, 1)
            }
        }
    }
    else{
        msgbox(RivalBrendanLikeOutfit, MSGBOX_YESNO)
        if(var(VAR_RESULT) == FALSE){
            if(flag(FLAG_PC_CHANGE_COSTUME)){
                msgbox(DontLikeBrendansOutfitV2)
            }
            else{
                msgbox(DontLikeBrendansOutfit)
            }
            clearflag(FLAG_MAY_COSTUME_CHANGE)
            clearflag(FLAG_BRENDAN_COSTUME_CHANGE)
            if(var(VAR_RIVAL_AROUND_SANDSTONE) == 0){
                setvar(VAR_RIVAL_AROUND_SANDSTONE, 2)
            }
        }
        else{
            if(flag(FLAG_PC_CHANGE_COSTUME)){
                msgbox(LikeBrendansOutfitV2)
            }
            else{
                msgbox(LikeBrendansOutfit)
            }
            if(var(VAR_RIVAL_AROUND_SANDSTONE) == 0){
                setvar(VAR_RIVAL_AROUND_SANDSTONE, 1)
            }
        }
    }
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Sandstone_PlayerWatchRivalExit)
	applymovement(13, Sandstone_RivalExitAfterStore)
	waitmovement(0)
	removeobject(13)
    setvar(VAR_SLATEPORT_OUTSIDE_MUSEUM_STATE, 3)
    release
    end
}

movement Sandstone_PushPlayerDown{
	lock_facing_direction
	walk_down
	unlock_facing_direction
	step_end
}

movement Sandstone_PlayerWatchRivalExit{
	delay_16 * 2
	walk_in_place_faster_left
	step_end
}

movement Sandstone_RivalExitStore{
	delay_8
	walk_down
	step_end
}

movement Sandstone_RivalExitAfterStore{
    walk_left
    walk_down
    walk_left
	walk_down
	walk_left * 6
	step_end
}

text RivalMayLikeOutfit{
    "{RIVAL}: Oh, hi {PLAYER}!\n"
    "Were you going shopping too?\p"
    "I was just getting some essential items\n"
    "when I noticed this outfit on sale.\p"
    "Doesn't it look cute or what?"
}
text DontLikeMaysOutfit{
    "{RIVAL}: Oh… But I thought it was cute…\p"
    "No, you know what? It IS cute!\n"
    "You just don't understand fashion!\p"
    "Maybe you should think about switching\n"
    "that get-up of yours as well sometimes.\l"
    "Being on the road for such a long time\l"
    "has definitely left your outfit\l"
    "worse for wear!"
}
text LikeMaysOutfit{
    "{RIVAL}: I knew you'd like it!\p"
    "I see your sense of fashion\n"
    "isn't completely outdated.\p"
    "You too should visit the clothing\n"
    "store. Maybe you'll find something\l"
    "you like.\p"
    "…Anyway, I need to get going now!\p"
    "See you!"
}
text RivalBrendanLikeOutfit{
    "{RIVAL}: Oh, hey {PLAYER}!\n"
    "Planning to do a bit of shopping?\p"
    "I was just stocking up on some\n"
    "essential items when I happened to\l"
    "notice this outfit on sale.\p"
    "Does it look cool or what?"
}
text DontLikeBrendansOutfit{
    "{RIVAL}: Oh… I thought you'd agree…\p"
    "Well, if you're looking to update\n"
    "your style, they've just opened a new\l"
    "clothing store.\l"
    "They're even having a sale right now!\p"
    "…Anyway, I need to get going.\p"
    "See you…"
}
text LikeBrendansOutfit{
    "{RIVAL}: Thanks! Guess my sense of\n"
    "fashion isn't completely outdated\l"
    "after all!\p"
    "If you're looking to change your style,\n"
    "they just opened a new clothing store\l"
    "here.\p"
    "Our clothes do take quite a bit of\n"
    "damage when we're on the road so\l"
    "changing to a new get-up isn't such\l"
    "a bad idea.\p"
    "…Anyway, I need to get going now.\p"
    "See you again soon!"
}

text DontLikeMaysOutfitV2{
    "{RIVAL}: Oh… But I thought it was cute…\p"
    "No, you know what? It IS cute!\n"
    "You just don't understand fashion!\p"
    "Maybe you should think about\n"
    "switching that get-up of yours\l"
    "as well sometimes--\p"
    "…Oh wait, you already have…\p"
    "Well, whatever!"
}
text LikeMaysOutfitV2{
    "{RIVAL}: I knew you'd like it!\p"
    "I see your sense of fashion\n"
    "isn't completely outdated.\p"
    "…Oh wait, I see you've already\n"
    "got some fancy new style!\p"
    "And here I thought I got to\n"
    "surprise you first.\p"
    "…Anyway, I need to get going now!\p"
    "See you!"
}
text DontLikeBrendansOutfitV2{
    "{RIVAL}: Oh… I thought you'd agree…\p"
    "Um, I think your outfit looks\n"
    "good on you though…\p"
    "…Anyway, I need to get going.\p"
    "See you…"
}
text LikeBrendansOutfitV2{
    "{RIVAL}: Thanks! Guess my sense of\n"
    "fashion isn't completely outdated\l"
    "after all!\p"
    "Looks like you've already found\n"
    "the new clothing store.\p"
    "I have to say, that outfit of yours\n"
    "looks good too!\p"
    "…Anyway, I need to get going now.\p"
    "See you again soon!"
}