mapscripts Desert_MapScripts{}

script DesertQuakesOn{
    setflag(FLAG_DESERT_STEPS)
    release
    end
}
script DesertQuakesOff{
    clearflag(FLAG_DESERT_STEPS)
    release
    end
}

script DesertEntrance{
    msgbox("This place is still work in progress.\n"
    "You're free to explore, though there's\l"
    "no trainers here and even the terrain\l"
    "is still under construction in places.\p"
    "Wild encounters should be working.", MSGBOX_NPC)
    end
}

script ScorchedDesert_TriggerQuakes{
    lockall
    random(9)
    if(flag(FLAG_DESERT_RUINS_OPENED)){
        random(30)
    }
    if(var(VAR_RESULT) <= 5){
        addvar(VAR_TEMP_DESERTRUINSFOUND, 1)
        switch(var(VAR_TEMP_DESERTRUINSFOUND)){
        case 1:
            playse(SE_SWITCH)
            setvar(VAR_0x8004, 1)  // vertical pan
            setvar(VAR_0x8005, 1)  // horizontal pan
            setvar(VAR_0x8006, 6)  // num shakes
            setvar(VAR_0x8007, 6)  // shake delay
            special(ShakeCamera)
            waitstate
            msgbox("You can feel the ground\nshaking slightly.", MSGBOX_DEFAULT)
            waitmessage
            closemessage
        case 2:
            playse(SE_SWITCH)
            setvar(VAR_0x8004, 1)  // vertical pan
            setvar(VAR_0x8005, 1)  // horizontal pan
            setvar(VAR_0x8006, 10)  // num shakes
            setvar(VAR_0x8007, 6)  // shake delay
            special(ShakeCamera)
            waitstate
            msgbox("The shaking seems to be growing\nin magnitude.", MSGBOX_DEFAULT)
            waitmessage
            closemessage
        case 3:
            playse(SE_M_STRENGTH)
            setvar(VAR_0x8004, 1)  // vertical pan
            setvar(VAR_0x8005, 1)  // horizontal pan
            setvar(VAR_0x8006, 14)  // num shakes
            setvar(VAR_0x8007, 6)  // shake delay
            special(ShakeCamera)
            waitstate
            msgbox("You notice the sand shifting below\nyour feet as the shaking intensifies.", MSGBOX_DEFAULT)
            waitmessage
            closemessage
        case 4:
            playse(SE_M_STRENGTH)
            setvar(VAR_0x8004, 1)  // vertical pan
            setvar(VAR_0x8005, 1)  // horizontal pan
            setvar(VAR_0x8006, 20)  // num shakes
            setvar(VAR_0x8007, 4)  // shake delay
            special(ShakeCamera)
            waitstate
            msgbox("The shaking is getting intense.", MSGBOX_DEFAULT)
            waitmessage
            closemessage
        case 5:
            playse(SE_M_EARTHQUAKE)
            setvar(VAR_0x8004, 2)  // vertical pan
            setvar(VAR_0x8005, 2)  // horizontal pan
            setvar(VAR_0x8006, 30)  // num shakes
            setvar(VAR_0x8007, 3)  // shake delay
            special(ShakeCamera)
            waitstate
            waitse
            delay(30)
            closemessage
            setvar(VAR_TEMP_DESERTRUINSFOUND, 0)
            delay(10)
            playse(SE_SWITCH)
            getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
            setmetatile(VAR_TEMP_1, VAR_TEMP_2, 0x3DF, FALSE)
            setobjectxy(OBJ_EVENT_ID_PLAYER, VAR_TEMP_1, VAR_TEMP_2)
            special(DrawWholeMapView)
            goto(ScorchedDesert_FallThroughFloor)
        }
    }
    releaseall
    end
}
script ScorchedDesert_FallThroughFloor{
	lockall
	delay(20)
	applymovement(OBJ_EVENT_ID_PLAYER, ScorchedDesert_FallThroughFloorMovement)
	waitmovement(0)
	playse(SE_FALL)
	delay(60)
    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
    if ((var(VAR_TEMP_1) <= 31) && (var(VAR_TEMP_2) <= 37)){
        warpholexy(MAP_UNDERGROUND_RUINS, 5, 4)
    }
    elif ((var(VAR_TEMP_1) > 31) && (var(VAR_TEMP_2) > 37)){
        warpholexy(MAP_UNDERGROUND_RUINS, 79, 57)
    }
    elif ((var(VAR_TEMP_1) <= 31) && (var(VAR_TEMP_2) > 37)){
        warpholexy(MAP_UNDERGROUND_RUINS, 24, 63)
    }
    elif ((var(VAR_TEMP_1) > 31) && (var(VAR_TEMP_2) <= 37)){
        warpholexy(MAP_UNDERGROUND_RUINS, 73, 4)
    }
    else{
        msgbox("something borked")
    }
	waitstate
	end
}
movement ScorchedDesert_FallThroughFloorMovement{
	set_invisible
	step_end
}

script DesertItem1{
    finditem(ITEM_TM49)
    end
}
script DesertItem2{
    finditem(ITEM_HEART_SCALE)
    end
}
script DesertItem3{
    finditem(ITEM_ELIXIR)
    end
}

script Desert_Trainer1_Camper {
    trainerbattle_single(TRAINER_DESERT_TR1_CAMPER, Desert_Trainer1_Camper_Intro, Desert_Trainer1_Camper_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer1_Camper_PostBattle, MSGBOX_AUTOCLOSE)
    end
}
text Desert_Trainer1_Camper_Intro{
    "The desert climate can be tough,\n"
    "but I'm even tougher."
}
text Desert_Trainer1_Camper_Defeat{
    "Not… tough… enough…"
}
text Desert_Trainer1_Camper_PostBattle{
    "Take care when exploring the desert.\n"
    "The sandstorm can be vicious\l"
    "and the land treacherous."
}

script Desert_Trainer2_Camper2 {
    trainerbattle_single(TRAINER_DESERT_TR2_CAMPER2, Desert_Trainer2_Camper2_Intro, Desert_Trainer2_Camper2_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer2_Camper2_PostBattle, MSGBOX_AUTOCLOSE)
    end
}

text Desert_Trainer2_Camper2_Intro{
    "I'm looking for desert treasures,\n"
    "but looks like I found a trainer battle\l"
    "instead!"
}
text Desert_Trainer2_Camper2_Defeat{
    "I should probably stick to\n"
    "treasure hunting…"
}
text Desert_Trainer2_Camper2_PostBattle{
    "There can be all sorts of treasures\n"
    "buried in the sand.\l"
    "You never know what you'll find."
}

script Desert_Trainer3_Picnicker {
    trainerbattle_single(TRAINER_DESERT_TR3_PICNICKER, Desert_Trainer3_Picnicker_Intro, Desert_Trainer3_Picnicker_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer3_Picnicker_PostBattle, MSGBOX_AUTOCLOSE)
    end
}

text Desert_Trainer3_Picnicker_Intro{
    "I'm on a patrol.\n"
    "I'll check your strength as well!"
}
text Desert_Trainer3_Picnicker_Defeat{
    "You were so strong\n"
    "that I couldn't help falling back!"
}
text Desert_Trainer3_Picnicker_PostBattle{
    "Nothing scares me\n"
    "when I patrol with my Pokémon!"
}

script Desert_Trainer4_Picnicker2 {
    trainerbattle_single(TRAINER_DESERT_TR4_PICNICKER2, Desert_Trainer4_Picnicker2_Intro, Desert_Trainer4_Picnicker2_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer4_Picnicker2_PostBattle, MSGBOX_AUTOCLOSE)
    end
}

text Desert_Trainer4_Picnicker2_Intro{
    "Straying off the beaten path…\n"
    "You must be searching for a battle!"
}
text Desert_Trainer4_Picnicker2_Defeat{
    "How could I lose…\n"
    "This is ridiculous!"
}
text Desert_Trainer4_Picnicker2_PostBattle{
    "The desert can be hard to navigate.\p"
    "If you ever become lost, try to find\n"
    "other people and follow the path\l"
    "they're taking."
}

script Desert_Trainer5_Hiker {
    trainerbattle_single(TRAINER_DESERT_TR5_HIKER, Desert_Trainer5_Hiker_Intro, Desert_Trainer5_Hiker_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer5_Hiker_PostBattle, MSGBOX_AUTOCLOSE)
    end
}

text Desert_Trainer5_Hiker_Intro{
    "How tough are you?\n"
	"We shall expose that secret!"
}
text Desert_Trainer5_Hiker_Defeat{
    "Oh! Your strength!\n"
	"It is shrouded in mystery!"

}
text Desert_Trainer5_Hiker_PostBattle{
    "This desert hoards mysteries in\n"
	"its shifting sands!"

}

script Desert_Trainer6_Blackbelt {
    trainerbattle_single(TRAINER_DESERT_TR6_BLACKBELT, Desert_Trainer6_Blackbelt_Intro, Desert_Trainer6_Blackbelt_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer6_Blackbelt_PostBattle, MSGBOX_AUTOCLOSE)
    end
}

text Desert_Trainer6_Blackbelt_Intro{
    "I will crush any who dare\n"
    "challenge me!"
}
text Desert_Trainer6_Blackbelt_Defeat{
    "My strength wanes…"
}
text Desert_Trainer6_Blackbelt_PostBattle{
    "This little sandstorm doesn't\n"
    "intimidate me.\p"
    "Sand is just tiny rocks, and I'm\n"
    "the master of breaking rocks."
}

script Desert_Trainer7_Picnicker3 {
    trainerbattle_single(TRAINER_DESERT_TR7_PICNICKER3, Desert_Trainer7_Picnicker3_Intro, Desert_Trainer7_Picnicker3_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer7_Picnicker3_PostBattle, MSGBOX_AUTOCLOSE)
    end
}

text Desert_Trainer7_Picnicker3_Intro{
    "Are you here to train?\n"
    "Care to train against me?"
}
text Desert_Trainer7_Picnicker3_Defeat{
    "Good hustle."
}
text Desert_Trainer7_Picnicker3_PostBattle{
    "Training here is great, just be\n"
    "careful of the wild Trapinch.\p"
    "They like to surprise you and\n"
    "bite at your ankles."
}

script Desert_Trainer8_Hiker2 {
    trainerbattle_single(TRAINER_DESERT_TR8_HIKER2, Desert_Trainer8_Hiker2_Intro, Desert_Trainer8_Hiker2_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer8_Hiker2_PostBattle, MSGBOX_AUTOCLOSE)
    end
}

text Desert_Trainer8_Hiker2_Intro{
    "Who might you be?\p"
	"Are you perhaps searching for\n"
    "ancient ruins?\n"

}
text Desert_Trainer8_Hiker2_Defeat{
    "What a disgraceful setback…"

}
text Desert_Trainer8_Hiker2_PostBattle{
    "There's a set of small ancient ruins to\n"
    "the northeast of us but I believe\l"
    "the sands might be hiding even more\l"
    "ruins beneath."
}

script Desert_Trainer9_Hiker3 {
    trainerbattle_single(TRAINER_DESERT_TR9_HIKER3, Desert_Trainer9_Hiker3_Intro, Desert_Trainer9_Hiker3_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer9_Hiker3_PostBattle, MSGBOX_AUTOCLOSE)
    end
}

text Desert_Trainer9_Hiker3_Intro{
    "The sand erodes mountains,\n"
    "but my Pokémon still stand!"
}
text Desert_Trainer9_Hiker3_Defeat{
    "Such power…!"
}
text Desert_Trainer9_Hiker3_PostBattle{
    "I have no doubt your Pokémon could\n"
    "withstand even a tsunami!"
}

script Desert_Trainer10_Firebreather {
    trainerbattle_single(TRAINER_DESERT_TR10_FIREBREATHER, Desert_Trainer10_Firebreather_Intro, Desert_Trainer10_Firebreather_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer10_Firebreather_PostBattle, MSGBOX_AUTOCLOSE)
    end
}

text Desert_Trainer10_Firebreather_Intro{
    "Help, I can't see anything\n"
    "in this storm!\l"
    "Are you a trainer?\l"
    "Want to have a battle?"
}
text Desert_Trainer10_Firebreather_Defeat{
    "I can't see! Is it over?\n"
    "Did we lose?"
}
text Desert_Trainer10_Firebreather_PostBattle{
    "I should have never come to this\n"
    "stinking desert without eyewear!"
}

script Desert_Trainer11_Blackbelt2 {
    trainerbattle_single(TRAINER_DESERT_TR11_BLACKBELT2, Desert_Trainer11_Blackbelt2_Intro, Desert_Trainer11_Blackbelt2_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer11_Blackbelt2_PostBattle, MSGBOX_AUTOCLOSE)
    end
}

text Desert_Trainer11_Blackbelt2_Intro{
    "A punch from my Pokémon can shift\n"
    "even the mountains!"
}
text Desert_Trainer11_Blackbelt2_Defeat{
    "Impossible!"
}
text Desert_Trainer11_Blackbelt2_PostBattle{
    "We must train even harder!\n"
    "Hundred push-ups, Hundred sit-ups,\l"
    "Hundred squats, ten kilometer run!\l"
    "Every day!!"
}

script Desert_Trainer12_Hiker4 {
    trainerbattle_single(TRAINER_DESERT_TR12_HIKER4, Desert_Trainer12_Hiker4_Intro, Desert_Trainer12_Hiker4_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer12_Hiker4_PostBattle, MSGBOX_AUTOCLOSE)
    end
}

text Desert_Trainer12_Hiker4_Intro{
    "For thirty years I have searched for\n"
	"ancient ruins!\l"
	"I am to be challenged?"

}
text Desert_Trainer12_Hiker4_Defeat{
    "While I have searched for ruins,\n"
	"I've not searched for strong Pokémon."

}
text Desert_Trainer12_Hiker4_PostBattle{
    "For thirty years I have searched for\n"
	"ancient ruins!\p"
	"No, wait, was that forty years?\n"
	"Which was it now?"
}

script Desert_Trainer13_Psychic_F {
    trainerbattle_single(TRAINER_DESERT_TR13_PSYCHIC_F, Desert_Trainer13_Psychic_F_Intro, Desert_Trainer13_Psychic_F_Defeat, DesertTrainer_IncreaseVar)
    msgbox(Desert_Trainer13_Psychic_F_PostBattle, MSGBOX_AUTOCLOSE)
    end
}

text Desert_Trainer13_Psychic_F_Intro{
    "I see a Pokémon battle defeat\n"
    "in your future."
}
text Desert_Trainer13_Psychic_F_Defeat{
    "I guess the defeat I saw was my own…"
}
text Desert_Trainer13_Psychic_F_PostBattle{
    "I don't need goggles to see in this\n"
    "sandstorm.\p"
    "My mind's eye shows me all I need\n"
    "to see."
}

script Desert_QuakesAbout{
    if(flag(FLAG_DESERT_RUINS_OPENED)){
        msgbox(Desert_QuakesAbout_LessQuakes, MSGBOX_NPC)
        end
    }
    msgbox(Desert_QuakesAbout_Text, MSGBOX_NPC)
    end
}
text Desert_QuakesAbout_Text{
    "There have been reports of seismic\n"
    "activity around the desert since\l"
    "time immemorial.\p"
    "Some say these ruins are protected from\n"
    "these quakes while some experts believe\l"
    "that their presence is causing tension\l"
    "underneath the surface, amplifying the\l"
    "magnitude of the tremors."
}
text Desert_QuakesAbout_LessQuakes{
    "Not too long ago there was a weird\n"
    "tremor right beneath our feet.\p"
    "Ever since then, the overall seismic\n"
    "readings seem to have gone down\l"
    "drastically."
}

script DesertTrainer_IncreaseVar{
    addvar(VAR_DESERT_TRAINERS, 1)
    release
    end
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////

script Desert_Trainer14_Cooltrainer{
    if(var(VAR_DESERT_TRAINERS) >= 14){
        msgbox(Desert_Trainer14_Cooltrainer_PostBattle, MSGBOX_NPC)
        end
    }
    elif(var(VAR_DESERT_TRAINERS) <= 12){
        msgbox(Desert_Trainer14_Cooltrainer_DefeatMoreTrainers, MSGBOX_NPC)
        end
    }
    else{
        goto(Desert_Trainer14_CooltrainerEligibleForBattle)
    }
}
script Desert_Trainer14_CooltrainerEligibleForBattle{
    lockall
    faceplayer
    if(!flag(FLAG_TEMP_5)){
        setflag(FLAG_TEMP_5)
        msgbox(Desert_Trainer14_Cooltrainer_ReadyForBattle, MSGBOX_YESNO)
    }
    else{
        msgbox(Desert_Trainer14_Cooltrainer_ReadyForBattleShort, MSGBOX_YESNO)
    }
    if(var(VAR_RESULT) == YES){
        msgbox("Yay! Let's go!", MSGBOX_DEFAULT)
        trainerbattle_no_intro(TRAINER_DESERT_TR14_COOLTRAINER, Desert_Trainer14_CooltrainerDefeated)
        goto(Desert_Trainer14_CooltrainerPostBattle)
	    end
    }
    if(var(VAR_RESULT) == NO){
        msgbox(Desert_Trainer14_Cooltrainer_DeclineBattle, MSGBOX_NPC)
        end
    }
}
script Desert_Trainer14_CooltrainerPostBattle{
    msgbox(Desert_Trainer14_CooltrainerReward, MSGBOX_DEFAULT)
    giveitem(ITEM_SAFETY_GOGGLES)
    waitfanfare
    msgbox(Desert_ExplainReward, MSGBOX_DEFAULT)
    call(Desert_GiveBP)
    setvar(VAR_DESERT_TRAINERS, 14)
    playfanfare(MUS_OBTAIN_B_POINTS)
    msgbox("Earned {STR_VAR_1} BP!", MSGBOX_DEFAULT)
    waitfanfare
    if(!flag(FLAG_UNUSED_BP_INTRO)){
        msgbox(ExplainBP, MSGBOX_DEFAULT)
        setflag(FLAG_UNUSED_BP_INTRO)
    }
    waitmessage
    closemessage
    release
	end
}
script Desert_GiveBP{
    buffernumberstring (STR_VAR_1, 20)
    setvar(VAR_0x8004, 20)
    special(GiveFrontierBattlePoints)
}
text Desert_Trainer14_Cooltrainer_ReadyForBattle{
    "I'm so bored…\p"
    "I came to the desert because I heard\n"
    "there's a lot of strong trainers\l"
    "training here but not one of them had\l"
    "a chance against me.\l"
    "You on the other hand look like someone\l"
    "who could entertain me.\p"
    "How about it,\n"
    "are you up for a Pokémon battle?\p"
    "I'm pretty strong so I hope\n"
    "you're prepared!"
}
text Desert_Trainer14_Cooltrainer_ReadyForBattleShort{
    "You're back!\n"
    "Can we please have a Pokémon battle?"
}
text Desert_Trainer14_Cooltrainer_DeclineBattle{
    "Aww…\p"
    "Please let me know if you change your\n"
    "mind. I'm itching for a real battle!"
}
text Desert_Trainer14_Cooltrainer_PostBattle{
    "My Pokémon and I need to train now.\n"
    "I hope we can have a rematch once we're\l"
    "a little bit stronger!"
}
text Desert_Trainer14_Cooltrainer_DefeatMoreTrainers{
    "I'm so bored…\p"
    "I want to fight strong trainers but\n"
    "none of the trainers here could match\l"
    "my awesomeness…\p"
    "You look kinda strong but I'm not\n"
    "so sure… "
}
text Desert_Trainer14_CooltrainerDefeated{
    "Even though we lost, I don't feel\n"
    "a single bit of resentment.\p"
    "That was a really fun battle!\n"
    "Can we go again?"
}
text Desert_Trainer14_CooltrainerReward{
    "That was awesome!\p"
    "It's been a long time since I've met\n"
    "anyone who could rival me!\l"
    "I'm so pumped!\p"
    "You know what? You were such an awesome\n"
    "sport, I want you to take these!"
}
text Desert_ExplainReward{
    "If you give those goggles to a Pokémon,\n"
    "they will be protected from powder and\l"
    "spore moves and will not take damage\l"
    "from weather effects like hail or\l"
    "sandstorm!\p"
    "…Oh, and take these as well!"
}

script TestVarDesert{
    buffernumberstring(STR_VAR_1, VAR_DESERT_TRAINERS)
    msgbox("{STR_VAR_1}", MSGBOX_NPC)
    end
}