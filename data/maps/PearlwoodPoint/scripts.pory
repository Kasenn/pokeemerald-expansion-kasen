mapscripts PearlwoodPoint_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: Pearlwood_Visited
}

script Pearlwood_Visited{
    setflag(FLAG_VISITED_PEARLWOOD_POINT)
}

script Pearlwood_Point_NoPokemon1{
    setvar(VAR_TEMP_1, 1)
    goto(Pearlwood_Point_NoPokemon)
}

script Pearlwood_Point_NoPokemon2{
    setvar(VAR_TEMP_1, 2)
    goto(Pearlwood_Point_NoPokemon)
}

script Pearlwood_Point_NoPokemon{
    lock
    turnobject(2, DIR_SOUTH)
    playse(SE_PIN)
    applymovement(2, Common_Movement_ExclamationMark)
    waitmovement(0)
    delay(48)
    msgbox(Pearlwood_Point_WatchOut)
    closemessage
    if(var(VAR_TEMP_1) == 1){
        turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    }
    else{
        applymovement(OBJ_EVENT_ID_PLAYER, Walk1Up)
        waitmovement(0)
    }
    msgbox(Pearlwood_Point_WildPokemon)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Walk1Left)
    waitmovement(0)
    release
}
text Pearlwood_Point_WatchOut{
    "Wait!"
}
text Pearlwood_Point_WildPokemon{
    "There are a lot of wild\n"
    "Pokémon living outside the town!\p"
    "It's dangerous to leave the town\n"
    "without your own Pokémon."
}

script Pearlwood_Point_StopGirlTalk{
    msgbox(Pearlwood_Point_Dangerous, MSGBOX_NPC)
    turnobject(2, DIR_SOUTH)
}
text Pearlwood_Point_Dangerous{
    "It's dangerous to leave the town\n"
    "without your own Pokémon."
}

script Pearlwood_BrotherFightRight{
    setvar(VAR_TEMP_2, 1)
    goto(Pearlwood_BrotherFightMain)
}
script Pearlwood_BrotherFightLeft{
    setvar(VAR_TEMP_2, 2)
    goto(Pearlwood_BrotherFightMain)
}

script Pearlwood_BrotherFightMain{
    lock
    playse(SE_PIN)
    msgbox("???: {PLAYER}!")
    closemessage
    delay(30)
    if(var(VAR_TEMP_2) == 1){
        applymovement(3, Walk5Up)
    }
    else{
        applymovement(3, Walk1Left)
        waitmovement(0)
        applymovement(3, Walk5Up)
    }
    waitmovement(0)
    msgbox(BrotherFightIntro)
    setflag(FLAG_CONTINUE_AFTER_WHITEOUT)
    switch(var(VAR_STARTER_MON)){
        case 0:
            trainerbattle_single(TRAINER_BROTHER1_ROWLET, Pearlwood_Brother_Intro, Pearlwood_Brother_Outro, PearlwoodWon)
        case 1:
            trainerbattle_single(TRAINER_BROTHER1_TORCHIC, Pearlwood_Brother_Intro, Pearlwood_Brother_Outro, PearlwoodWon)
        case 2:
            trainerbattle_single(TRAINER_BROTHER1_PIPLUP, Pearlwood_Brother_Intro, Pearlwood_Brother_Outro, PearlwoodWon)
    }
}

text BrotherFightIntro{
    "{BROTHER}: I see you got your Pokémon\n"
    "from the professor.\p"
    "Well? What're we waiting for?"
}

text Pearlwood_Brother_Intro{
    "Let's see if it's any good\n"
    "in a Pokémon battle!\p"
    "Oh, don't worry. I won't use the same\n"
    "Pokémon I used during my fight against\l"
    "the Kanto Pokémon League.\p"
    "That would be simply unfair.\p"
    "However, don't expect any mercy from\n"
    "me either!"
}

text Pearlwood_Brother_Outro{
    "Of course you picked THAT one!"
}

script PearlwoodWon{
    if(var(VAR_RESULT) == B_OUTCOME_WON){
        msgbox(PearlwoodBrotherPrize)
    }
	else{
        msgbox(PlayerHouse1F_Apology)
    }
    closemessage
    clearflag(FLAG_CONTINUE_AFTER_WHITEOUT)
    special(HealPlayerParty)
    applymovement(3, Walk5Down)
	waitmovement(0)
    removeobject(3)
    setvar(VAR_PEARLWOOD_TOWN_STATE, 6)
	release
}

text PearlwoodBrotherPrize{
    "Hmph… I guess I should not\n"
    "have underestimated you.\p"
    "Anyway, have you thought about what\n"
    "you're going to do next?\p"
    "If you're up for it, we should totally\n"
    "see which of us is the better trainer\l"
    "by taking on the Pokémon Gym Challenge!\p"
    "Our neighbor {RIVAL} just set out this\n"
    "morning so we better get moving or\l"
    "we'll fall behind!\p"
    "Don't know about you but I'm not\n"
    "content with a second place.\p"
    "Catch ya later!"
}

script Pearlwood_Point_FatMan{
    msgbox(Pearlwood_Point_FatMan_Text, MSGBOX_NPC)
}
text Pearlwood_Point_FatMan_Text{
    "The weather is so calm here despite\n"
    "the close proximity to the sea.\p"
    "Must be all the trees blocking\n"
    "the sea breeze."
}

script Pearlwood_Point_LittleGirl{
    msgbox(Pearlwood_Point_LittleGirl_Text, MSGBOX_NPC)
    turnobject(4, DIR_NORTH)
}
text Pearlwood_Point_LittleGirl_Text{
    "Shh!\p"
    "I think I hear the cry of a wild\n"
    "Pokémon coming from over there."
}

script Pearlwood_Sign{
    msgbox(Pearlwood_Sign_Text, MSGBOX_SIGN)
}
text Pearlwood_Sign_Text{
    "PEARLWOOD POINT\n"
    "“A town of new beginnings.”\p"
    "{LEFT_ARROW} Prof. Birch's Pokémon lab\n"
    "{RIGHT_ARROW} Route 1"
}
