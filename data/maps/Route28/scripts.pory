mapscripts Route28_MapScripts{}

script Route28_BrotherTrigger_1{
    if(flag(FLAG_ROUTE28_DESPAWN_BROTHER)){
        release
        end
    }
    else{
        lock
        playbgm(MUS_ENCOUNTER_BRENDAN, FALSE)
        safefollow
        applymovement(4, Route28_MoveBrother1)
        waitmovement(4)
        delay(15)
        call(Route28_BrotherFight)
        safefollow
        applymovement(4, Route28BrotherLeaves1)
        goto(Route28_EndScript)
    }
}
script Route28_BrotherTrigger_2{
    if(flag(FLAG_ROUTE28_DESPAWN_BROTHER)){
        release
        end
    }
    else{
        lock
        playbgm(MUS_ENCOUNTER_BRENDAN, FALSE)
        safefollow
        applymovement(4, Route28_MoveBrother2)
        waitmovement(4)
        delay(15)
        call(Route28_BrotherFight)
        safefollow
        applymovement(4, Route28BrotherLeaves2)
        goto(Route28_EndScript)
    }
}

script Route28_EndScript{
    waitmovement(4)
    delay(15)
    setflag(FLAG_ROUTE28_DESPAWN_BROTHER)
    removeobject(4)
    savebgm(MUS_DUMMY)
	fadedefaultbgm
    releaseall
    end
}

movement Route28_MoveBrother1{
    walk_down * 2
    walk_left * 3
    walk_down * 3
}
movement Route28_MoveBrother2{
    walk_down * 2
    walk_left * 2
    walk_down * 3
}
movement Route28BrotherLeaves1{
    walk_right
    walk_down * 8
}
movement Route28BrotherLeaves2{
    walk_left
    walk_down * 8
}

script Route28_BrotherFight{
    delay(5)
    if(flag(FLAG_BADGE02_GET) && (!flag(FLAG_RECEIVED_HM_FLY))){
        msgbox(Route28_BrotherIntroAfterGym, MSGBOX_DEFAULT)
        waitmessage
        closemessage
        switch(var(VAR_STARTER_MON)){
        case 0:
            trainerbattle_no_intro(TRAINER_BROTHER3_ROWLET, Route28_BrotherLost)
        case 1:
            trainerbattle_no_intro(TRAINER_BROTHER3_TORCHIC, Route28_BrotherLost)
        case 2:
            trainerbattle_no_intro(TRAINER_BROTHER3_PIPLUP, Route28_BrotherLost)
        }
    }
    elif(flag(FLAG_BADGE02_GET) && (flag(FLAG_RECEIVED_HM_FLY))){
        msgbox(Route28_BrotherIntroAfterGym, MSGBOX_DEFAULT)
        waitmessage
        closemessage
        switch(var(VAR_STARTER_MON)){
        case 0:
            trainerbattle_no_intro(TRAINER_BROTHER4_ROWLET, Route28_BrotherLost)
        case 1:
            trainerbattle_no_intro(TRAINER_BROTHER4_TORCHIC, Route28_BrotherLost)
        case 2:
            trainerbattle_no_intro(TRAINER_BROTHER4_PIPLUP, Route28_BrotherLost)
        }
    }
    else{
        msgbox(Route28_BrotherIntro, MSGBOX_DEFAULT)
        waitmessage
        closemessage
        switch(var(VAR_STARTER_MON)){
        case 0:
            trainerbattle_no_intro(TRAINER_BROTHER2_ROWLET, Route28_BrotherLost)
        case 1:
            trainerbattle_no_intro(TRAINER_BROTHER2_TORCHIC, Route28_BrotherLost)
        case 2:
            trainerbattle_no_intro(TRAINER_BROTHER2_PIPLUP, Route28_BrotherLost)
        }
    }
    if(flag(FLAG_BADGE02_GET)){
        msgbox(Route28_BrotherPostBattle, MSGBOX_DEFAULT)
    }
    else{
        msgbox(Route28_BrotherPostBattleNoBadge, MSGBOX_DEFAULT)
    }
    waitmessage
    closemessage
    return
}

text Route28_BrotherIntro{
    "{BROTHER}: Yo, {PLAYER}!\p"
    "I see you've finally made your way\n"
    "off the Coralgrove Islands.\p"
    "I wanted to head north to check out\n"
    "the Pokémon League in Snowcrest City\l"
    "but there's been an avalanche\l"
    "higher up the route so I couldn't\l"
    "even reach Iceperch City.\p"
    "In any case, I've gone and caught\n"
    "a lot of new Pokémon.\p"
    "Let's see if yours are any match\n"
    "to them!"
}

text Route28_BrotherIntroAfterGym{
    "{BROTHER}: Yo, {PLAYER}!\p"
    "I see you've finally made your way\n"
    "off the Coralgrove Islands.\p"
    "I wanted to head north to check out\n"
    "the Pokémon League in Snowcrest City\l"
    "but there's been an avalanche\l"
    "higher up the route so I couldn't\l"
    "even reach Iceperch City.\p"
    "I see you've already gone and\n"
    "obtained the Dynamo Badge from\l"
    "the Aldeleaf Gym.\p"
    "Good, that means you're not a complete\n"
    "newbie anymore.\p"
    "In any case, I've gone and caught\n"
    "a lot of new Pokémon.\p"
    "Let's see if yours are any match\n"
    "to them!"
}
text Route28_BrotherLost{
    "I…Hm…Not quite the outcome\n"
    "I expected…\l"
    "Beginner's luck, maybe?"
}
text Route28_BrotherPostBattle{
    "I see I clearly underestimated you.\n"
    "Looks like I need to actually\l"
    "start trying.\p"
    "Seeing how the route north is blocked,\n"
    "if I want to challenge even more gyms,\l"
    "I need to head south to Shoreslate City\l"
    "to take a ferry across the ocean.\l"
    "You may want to do the same.\p"
    "Anyway, catch ya later!"
}
text Route28_BrotherPostBattleNoBadge{
    "I see I clearly underestimated you.\n"
    "Looks like I need to actually\l"
    "start trying.\p"
    "Seeing how the route north is blocked,\n"
    "if I want to challenge even more gyms,\l"
    "I need to head south to Shoreslate City\l"
    "to take a ferry across the ocean.\p"
    "Seeing how much you've progressed,\n"
    "I think you should try taking on the\l"
    "Aldeleaf City Gym.\p"
    "Once you've obtained your second badge,\n"
    "you may want to head south as well.\p"
    "Anyway, catch ya later!"
}

script Route28_TM{
    finditem(ITEM_TM43)
    end
}
script Route28_ItemBall{
    finditem(ITEM_FULL_HEAL)
    end
}

script Route28_Trainer1_BugCatcher{
    trainerbattle_single(TRAINER_R28_TR1_BUGCATCHER, Route28_Trainer1_BugCatcherIntro, Route28_Trainer1_BugCatcherDefeat)
	msgbox(Route28_Trainer1_BugCatcherPostBattle, MSGBOX_AUTOCLOSE)
	end
}
script Route28_Trainer2_Hiker{
    trainerbattle_single(TRAINER_R28_TR2_HIKER, Route28_Trainer2_HikerIntro, Route28_Trainer2_HikerDefeat)
	msgbox(Route28_Trainer2_HikerPostBattle, MSGBOX_AUTOCLOSE)
	end
}
script Route28_Trainer3_NinjaBoy{
    trainerbattle_single(TRAINER_R28_TR3_NINJABOY, Route28_Trainer3_NinjaBoyIntro, Route28_Trainer3_NinjaBoyDefeat)
	msgbox(Route28_Trainer3_NinjaBoyPostBattle, MSGBOX_AUTOCLOSE)
	end
}
script Route28_Trainer4_Psychic{
    trainerbattle_single(TRAINER_R28_TR4_PSYCHIC, Route28_Trainer4_PsychicIntro, Route28_Trainer4_PsychicDefeat)
	msgbox(Route28_Trainer4_PsychicPostBattle, MSGBOX_AUTOCLOSE)
	end
}

text Route28_Trainer1_BugCatcherIntro{
    "You're not afraid of little bugs,\n"
    "are you?"
}
text Route28_Trainer1_BugCatcherDefeat{
    "How could this be?!"
}
text Route28_Trainer1_BugCatcherPostBattle{
    "Next time I'll have an army of bugs.\n"
    "You can count on that!"
}

text Route28_Trainer2_HikerIntro{
    "The path forward is blocked.\n"
    "Why not have a duel with me instead?"
}
text Route28_Trainer2_HikerDefeat{
    "Bwahaha! Good show!"
}
text Route28_Trainer2_HikerPostBattle{
    "I know your types, always wanting to\n"
    "explore every nook and cranny.\l"
    "I am much the same.\p"
    "Still, the road ahead is very unsafe.\n"
    "I recommend turning back while you can."
}

text Route28_Trainer3_NinjaBoyIntro{
    "I move at the speed of sound!"
}
text Route28_Trainer3_NinjaBoyDefeat{
    "Ninjas, scatter!"
}
text Route28_Trainer3_NinjaBoyPostBattle{
    "I must train more by staying here\n"
    "and ambushing any unsuspecting\l"
    "trainers to a Pokémon battle!"
}

text Route28_Trainer4_PsychicIntro{
    "Have you ever fought against\n"
    "psychic types before?\l"
    "Prepare to get overwhelmed!"
}
text Route28_Trainer4_PsychicDefeat{
    "I should've seen this…"
}
text Route28_Trainer4_PsychicPostBattle{
    "I'm sensing you want to explore\n"
    "the route ahead?\p"
    "Even without the avalanche blocking\n"
    "your way, I'm afraid the area might\l"
    "be way out of your league."
}

script Route28_Fisher{
    msgbox(Route28_Fisher_Text, MSGBOX_NPC)
    end
}
text Route28_Fisher_Text{
    "Route 28 is split in northern and\n"
    "southern sections.\p"
    "The northern section of the route goes\n"
    "high up into the mountains, making\l"
    "that part of the route quite snowy.\p"
    "Some of the terrain between the\n"
    "sections is incredibly rough so\l"
    "instead of forcing people to endanger\l"
    "themselves every time they want to\l"
    "get across, they opted to build\l"
    "a tunnel house that goes over\l"
    "the dangerous terrain instead."
}

script Route28_Sign{
    msgbox(Route28_Sign_Text, MSGBOX_SIGN)
}
text Route28_Sign_Text{
    "Trainer Tips\p"
    "During a battle, holding the START\n"
    "button and pressing up or down on\l"
    "the D-Pad changes the style of the\l"
    "window border, while pressing left or\l"
    "down on the D-Pad changes the color\l"
    "of the battle interface.\p"
    "During double battles, pressing the\n"
    "SELECT button switches the view\l"
    "between health bars and numbers."
}