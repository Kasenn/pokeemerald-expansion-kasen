mapscripts MarigorgeTown_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: MariGorgeSetFlag
}

script MariGorgeSetFlag{
    setflag(FLAG_VISITED_MARIGORGE)
    end
}

script Aldeleaf_MuseumCurator{
    lockall
    faceplayer
    if(var(VAR_TEMP_1) == 1){
        checkitem(ITEM_RELIC_COPPER)
        if(var(VAR_RESULT) == TRUE){
            goto(SellRelicShort)
        }
        checkitem(ITEM_RELIC_SILVER)
        if(var(VAR_RESULT) == TRUE){
            goto(SellRelicShort)
        }
        checkitem(ITEM_RELIC_GOLD)
        if(var(VAR_RESULT) == TRUE){
            goto(SellRelicShort)
        }
        else{
            msgbox(ThanksForContribution, MSGBOX_NPC)
            end
        }
    }
    else{
        msgbox(Aldeleaf_MuseumCurator_Intro, MSGBOX_DEFAULT)
        checkitem(ITEM_RELIC_COPPER)
        if(var(VAR_RESULT) == TRUE){
            goto(SellRelic)
        }
        checkitem(ITEM_RELIC_SILVER)
        if(var(VAR_RESULT) == TRUE){
            goto(SellRelic)
        }
        checkitem(ITEM_RELIC_GOLD)
        if(var(VAR_RESULT) == TRUE){
            goto(SellRelic)
        }
    }
    
    release
    end
}

script SellRelic{
    playse(SE_PIN)
    applymovement(2, Common_Movement_ExclamationMark)
    waitmovement(0)
    delay(40)
    msgbox(YouHaveARelicCoin, MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        msgbox(MuseumThatsAShame, MSGBOX_NPC)
        end
    }
    else{
        fadescreen(FADE_TO_BLACK)
        closemessage
        special(Bag_ChooseFossil)
        waitstate
        if(var(VAR_ITEM_ID) == 157){
            goto(SellCopper)
        }
        if(var(VAR_ITEM_ID) == 158){
            goto(SellSilver)
        }
        if(var(VAR_ITEM_ID) == 159){
            goto(SellGold)
        }
        else{
            msgbox(MuseumDontWantToSell, MSGBOX_NPC)
            end
        }
    }
}

script SellRelicShort{
    msgbox(HereToSellMore, MSGBOX_DEFAULT)
    fadescreen(FADE_TO_BLACK)
    closemessage
    special(Bag_ChooseFossil)
    waitstate
    if(var(VAR_ITEM_ID) == 157){
        goto(SellCopper)
    }
    if(var(VAR_ITEM_ID) == 158){
        goto(SellSilver)
    }
    if(var(VAR_ITEM_ID) == 159){
        goto(SellGold)
    }
    else{
        msgbox(MuseumDontWantToSell, MSGBOX_NPC)
        end
    } 
}

script SellCopper{
    showmoneybox(0, 0)
    msgbox(WouldYouLikeToSellCopper, MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        msgbox(MuseumThatsAShame, MSGBOX_NPC)
        end
    }
    else{
        addvar(VAR_RELIC_SOLD, 1)
        addmoney(1000)
        playse(SE_SHOP)
        updatemoneybox
        removeitem(ITEM_RELIC_COPPER, 1)
        setvar(VAR_TEMP_1, 1)
        msgbox(MuseumThanksForArtifact, MSGBOX_NPC)
        hidemoneybox
        end
    }
}
script SellSilver{
    showmoneybox(0, 0)
    msgbox(WouldYouLikeToSellSilver, MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        msgbox(MuseumThatsAShame, MSGBOX_NPC)
        end
    }
    else{
        addvar(VAR_RELIC_SOLD, 1)
        addmoney(5000)
        playse(SE_SHOP)
        updatemoneybox
        removeitem(ITEM_RELIC_SILVER, 1)
        setvar(VAR_TEMP_1, 1)
        msgbox(MuseumThanksForArtifact, MSGBOX_NPC)
        hidemoneybox
        end
    }
}
script SellGold{
    showmoneybox(0, 0)
    msgbox(WouldYouLikeToSellGold, MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        msgbox(MuseumThatsAShame, MSGBOX_NPC)
        end
    }
    else{
        addvar(VAR_RELIC_SOLD, 1)
        addmoney(10000)
        playse(SE_SHOP)
        updatemoneybox
        removeitem(ITEM_RELIC_GOLD, 1)
        setvar(VAR_TEMP_1, 1)
        msgbox(MuseumThanksForArtifact, MSGBOX_NPC)
        hidemoneybox
        end
    }
}

text Aldeleaf_MuseumCurator_Intro{
    "I'm the curator of this museum of\n"
	"fine arts.\p"
	"It's heartening to see someone so\n"
	"young as you in our museum.\p"
	"I bet you're curious about what\n"
	"lies up these stairs.\p"
	"Unfortunately we are still setting\n"
	"things up, so I can't let you pass."
}
text MuseumDontWantToSell{
    "Oh, so you're not looking to sell…"
}
text YouHaveARelicCoin{
    "You look like you've been to\n"
    "the desert.\p"
    "Have you found have any relic coins\n"
    "you'd be like to contribute to\l"
    "the museum?\l"
    "You will be compensated, of course."
}
text HereToSellMore{
    "Here to sell more relic coins?"
}
text ThanksForContribution{
    "Thank you for your contributions.\p"
    "We hope to have our new exhibition\n"
    "open as soon we have collected\l"
    "enough exhibits."
}
text WouldYouLikeToSellCopper{
    "That's a genuine Relic Copper!\p"
    "Would you like to sell it to\n"
    "the museum for ¥1000?"
}
text WouldYouLikeToSellSilver{
    "That's a genuine Relic Silver!\p"
    "Would you like to sell it to\n"
    "the museum for ¥5000?"
}
text WouldYouLikeToSellGold{
    "That's a genuine Relic Gold!\p"
    "Would you like to sell it to\n"
    "the museum for ¥10000?"
}
text MuseumThatsAShame{
    "That's a shame…"
}
text MuseumThanksForArtifact{
    "The museum thanks you for your\n"
    "contribution.\p"
    "Let me know if you have more relics\n"
    "you'd like to sell."
}

script Marigorge_Man{
    msgbox(Marigorge_Man_Text, MSGBOX_NPC)
    end
}
text Marigorge_Man_Text{
    "Are you here to visit the Safari Zone?\p"
    "There you can find all sorts of Pokémon\n"
    "that are either rare or unavailable\l"
    "in other parts of Sinko."
}

script MariGorge_Woman{
    msgbox(MariGorge_Woman_Text, MSGBOX_NPC)
    end
}
text MariGorge_Woman_Text{
    "Thanks to the fresh water flowing in\n"
    "from the mountains,\l"
    "this normally arid area is teeming\l"
    "with life."
}

script Marigorge_Sign1{
    msgbox(Marigorge_Sign1_Text, MSGBOX_SIGN)
    end
}
text Marigorge_Sign1_Text{
    "Move Deleter's house\p"
	"“Unwanted Pokémon moves deleted.”"
}

script Marigorge_Sign2{
    msgbox(Marigorge_Sign2_Text, MSGBOX_SIGN)
    end
}
text Marigorge_Sign2_Text{
    "Marigorge Town\p"
    "“A peaceful oasis amidst mountains.”"
}

script MarigorgeHouse_OldWoman{
    msgbox(MarigorgeHouse_OldWoman_Text, MSGBOX_NPC)
    end
}
text MarigorgeHouse_OldWoman_Text{
    "Have you seen the huge waterfall\n"
    "outside?\p"
    "A legend says that a strong Pokémon\n"
    "lives up that waterfall,\l"
    "and that only worthy trainers are\l"
    "allowed to even approach it."
}

script Marigorge_Mart_Woman{
    msgbox(Marigorge_Mart_Woman_Text, MSGBOX_NPC)
    end
}
text Marigorge_Mart_Woman_Text{
    "I was hoping to bring some Safari Balls\n"
    "as a souvenir but I guess they don't\l"
    "sell them in regular Poké Marts…"
}

script Marigorge_Mart_Fisherman{
    msgbox(Marigorge_Mart_Fisherman_Text, MSGBOX_NPC)
    end
}
text Marigorge_Mart_Fisherman_Text{
    "The new BP shops are great but if\n"
    "you're in need of regular everyday\l"
    "supplies, you really can't go wrong\l"
    "with Poké Mart!"
}

script Common_OpenBag{
    fadescreen(FADE_TO_BLACK)
    closemessage
    special(Bag_ChooseFossil)
    waitstate
    return
}

script Marigorge_SusMan{
    if(flag(FLAG_DAILY_SAFARIBALL_BOUGHT)){
        msgbox(Marigorge_SusMan_AlreadyBought, MSGBOX_NPC)
        end
    }
    lockall
    faceplayer
    msgbox(Marigorge_SusMan_Text, MSGBOX_DEFAULT)
    showmoneybox(0,0)
    msgbox(Marigorge_SusMan_BuyBall, MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        msgbox(Marigorge_SusMan_YourLoss, MSGBOX_NPC)
        hidemoneybox
        end
    }
    else{
        checkmoney(3000)
        if(var(VAR_RESULT) == FALSE){
            msgbox(Marigorge_SusMan_ScammingMe, MSGBOX_NPC)
            hidemoneybox
            end
        }
        checkitemspace(ITEM_SAFARI_BALL, 1)
        if (var(VAR_RESULT) == FALSE){
            msgbox(NotEnoughRoom, MSGBOX_NPC)
            hidemoneybox
            end
        }
        removemoney(3000)
        playse(SE_SHOP)
        updatemoneybox
        waitse
        msgbox(Marigorge_SusMan_GaveMoney, MSGBOX_DEFAULT)
        waitmessage
        closemessage
        hidemoneybox
        giveitem(ITEM_SAFARI_BALL, 1)
        setflag(FLAG_DAILY_SAFARIBALL_BOUGHT)
        msgbox(Marigorge_SusMan_DontGoToSafari, MSGBOX_NPC)
        end
    }
}

text Marigorge_SusMan_Text{
    "Psst!\p"
    "I've got a special deal,\n"
    "just for you.\p"
    "I was able to smuggle one of the\n"
    "Safari Balls out of the Safari Zone.\l"
    "Don't ask me how."
}
text Marigorge_SusMan_BuyBall{
    "Anyway, interested to buy it?\n"
    "I can sell it for ¥3000.\p"
    "It wasn't easy so of course the price\n"
    "must reflect the risk taken."
}
text Marigorge_SusMan_YourLoss{
    "Whatever man, your loss."
}
text Marigorge_SusMan_ScammingMe{
    "You trying to scam me or what?\n"
    "You clearly don't have enough money."
}
text Marigorge_SusMan_GaveMoney{
    "{PLAYER} gave ¥3000 to the\n"
    "suspicious man."
}
text Marigorge_SusMan_DontGoToSafari{
    "Pleasure doing business with you.\p"
    "If you're interested, I might be able\n"
    "to get another one tomorrow\l"
    "once the heat dies down."
}
text Marigorge_SusMan_AlreadyBought{
    "Sorry, but that's all I managed to\n"
    "smuggle out.\p"
    "I can maybe get another one tomorrow\n"
    "once the heat dies down."
}