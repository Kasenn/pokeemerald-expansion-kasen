mapscripts Lab2_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: Lab2_HideRival
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_PEARLWOOD_TOWN_STATE, 2: Lab2_Intro
        VAR_PEARLWOOD_TOWN_STATE, 3: Lab2_Intro
        VAR_PEARLWOOD_TOWN_STATE, 8: Lab2_DexGet
        VAR_PEARLWOOD_TOWN_STATE, 10: Lab2_GetMegaStone
    ]
}

script Lab2_GetMegaStone{
    clearflag(FLAG_HIDE_LAB_RIVAL)
    applymovement(OBJ_EVENT_ID_PLAYER, Walk6Up)
    waitmovement(0)
    msgbox(LabMegaStones)
    closemessage
    if(flag(FLAG_CORALGROVE_TRADE_COMPLETED)){
        playse(SE_PIN)
        applymovement(4, Common_Movement_QuestionMark)
        waitmovement(0)
        delay(30)
        msgbox(TradedAwayStarter)
        closemessage
    }
    checkitemspace(ITEM_DECIDUEITE)
    if(var(VAR_RESULT) == FALSE){
        msgbox(ComeGetYourStoneLater)
        setvar(VAR_PEARLWOOD_TOWN_STATE, 11)
    }
    else{
        switch(var(VAR_STARTER_MON)){
            case 0:
                giveitem(ITEM_DECIDUEITE)
            case 1:
                giveitem(ITEM_BLAZIKENITE)
            case 2:
                giveitem(ITEM_EMPOLEONITE)
        }
        setvar(VAR_PEARLWOOD_TOWN_STATE, 12)
        waitse
    }
    delay(24)
    closemessage
    applymovement(4, ProfBackAndForth)
    waitmovement(0)
    applymovement(5, Common_Movement_WalkInPlaceFasterLeft)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
    waitmovement(0)
    checkplayergender
    if(var(VAR_RESULT) == MALE){
        if(var(VAR_PEARLWOOD_TOWN_STATE) == 11){
            msgbox(GonnaGoAhead_May)
            applymovement(5, Common_Movement_WalkInPlaceFasterUp)
            waitmovement(0)
            msgbox(ThanksDadBirch)
        }
        else{
            msgbox(GonnaTakeMyLeave_May)
            applymovement(5, Common_Movement_WalkInPlaceFasterUp)
            waitmovement(0)
            msgbox(ThanksDadBirch)
        }
    }
    else{
        if(var(VAR_PEARLWOOD_TOWN_STATE) == 11){
            msgbox(GonnaGoAhead_Brendan)
            applymovement(5, Common_Movement_WalkInPlaceFasterUp)
            waitmovement(0)
            msgbox(ThanksDadBirch)
        }
        else{
            msgbox(GonnaTakeMyLeave_Brendan)
            applymovement(5, Common_Movement_WalkInPlaceFasterUp)
            waitmovement(0)
            msgbox(ThanksDadBirch)
        }
    }
    closemessage
    applymovement(5, RivalLeavesLab)
    applymovement(OBJ_EVENT_ID_PLAYER, TurnPlayer)
    waitmovement(5)
    removeobject(5)
    release
    end
}

text LabMegaStones{
    "Prof. Birch: {RIVAL}, {PLAYER},\n"
    "what a pleasant surprise.\l"
    "I was hoping to see you two soon.\p"
    "Thanks to a colleague of mine, I have\n"
    "been able to do a lot of research on\l"
    "a phenomena called “Mega Evolution.”\p"
    "Not much is known about it here in\n"
    "Sinko, but thanks to Professor\l"
    "Sycamore of Kalos, I have been\l"
    "able to greatly further my research.\p"
    "This is where you two come in.\p"
    "One day, I happened upon a mysterious\n"
    "set of stones. I believe these stones\l"
    "to be Mega Stones, special evolution\l"
    "stones capable of triggering a Mega\l"
    "Evolution within certain Pokémon.\p"
    "I would like you two to test out my\n"
    "theory, and see if your Pokémon are\l"
    "able to somehow interact with these\l"
    "Mega Stones.\p"
    "I want you both to have a stone that\n"
    "I believe is related to the Pokémon\l"
    "I have given you."
}
text TradedAwayStarter{
    "Prof. Birch: Oh, you haven't traded my\n"
    "magnanimous gift away, have you?\p"
    "Don't worry, I'm just kidding.\p"
    "I can still give you the stone that's\n"
    "associated with the Pokémon I gave you,\l"
    "but you'll have to figure out where to\l"
    "get the stone for the Pokémon you\l"
    "currently have by yourself.\p"
    "Maybe you could try checking with the\n"
    "person who traded you it originally?\p"
    "In any case, here you go."
}
text ComeGetYourStoneLater{
    "Prof. Birch: It seems your bag is\n"
    "currently too full to carry this stone.\p"
    "I will give {RIVAL}'s stone to them,\n"
    "and once you've made enough space in\l"
    "your bag, you too can have yours."
}
text GonnaGoAhead_May{
    "{RIVAL}: I can't wait to see this\n"
    "thing in action!\p"
    "You make sure you grab yours as well,\n"
    "{PLAYER}!\p"
    "I don't want to hear you whining that\n"
    "you lost because you didn't know how\l"
    "to use Mega Evolution!"
}
text GonnaTakeMyLeave_May{
    "{RIVAL}: I can't wait to see this\n"
    "thing in action!\p"
    "You better train your Pokémon to use\n"
    "the stone, {PLAYER}!\p"
    "I don't want to hear you whining that\n"
    "you lost because you didn't know how\l"
    "to use Mega Evolution!"
}
text GonnaGoAhead_Brendan{
    "{RIVAL}: I can't wait to see this\n"
    "thing in action!\p"
    "Make sure you grab yours before you\n"
    "leave, {PLAYER}.\p"
    "I don't want to hear any complaints\n"
    "about how you lost because you didn't\l"
    "know how to Mega Evolve your Pokémon."
}
text GonnaTakeMyLeave_Brendan{
    "{RIVAL}: I can't wait to see this\n"
    "thing in action!\p"
    "I hope you train your Pokémon to get\n"
    "the most out of that stone, {PLAYER}.\p"
    "I don't want to hear any complaints\n"
    "about how you lost because you didn't\l"
    "know how to Mega Evolve your Pokémon."
}
text ThanksDadBirch{
    "{RIVAL}: Thanks dad! I'm going to go\n"
    "and give this thing a try right away!"
}

movement ProfBackAndForth{
    walk_right
    walk_in_place_faster_down
    delay_16 * 6
    walk_left
    walk_in_place_faster_down
    step_end
}
movement RivalLeavesLab{
    walk_down * 7
}
movement TurnPlayer{
    walk_in_place_faster_right
    walk_in_place_faster_down
}

text YourFatherIsInTown{
    "Prof. Birch: Oh, I almost forgot!\n"
    "Your father is currently in town.\p"
    "I know he's a busy man. You may want\n"
    "to dash on home before he leaves."
}
text HeresYourStoneNow{
    "Prof. Birch: I hope you've made enough\n"
    "space in your bag to hold this stone."
}
text ComeGetYourStoneLaterShort{
    "No, looks like your bag is still full.\p"
    "You will need to make some room in your\n"
    "bag before I can give this to you."
}

script Lab2_Intro{
    applymovement(OBJ_EVENT_ID_PLAYER, Walk6Up)
    waitmovement(0)
    msgbox(Lab2_Intro_Speech)
    closemessage
    delay(20)
    turnobject(4, DIR_EAST)
    delay(20)
    msgbox(Lab2_PresentingBalls)
    closemessage
    delay(20)
    setvar(VAR_PEARLWOOD_TOWN_STATE, 4)
    release
}

script Lab2_HideRival{
    call(Common_EventScript_SetupRivalGfxId)
    if(flag(FLAG_SYS_POKEMON_GET)){
        switch(var(VAR_STARTER_MON)){
            case 0:
                setobjectxyperm(1, 20, 0)
            case 1:
                setobjectxyperm(2, 20, 0)
            case 2:
                setobjectxyperm(3, 20, 0)
        }
    }
    if(var(VAR_PEARLWOOD_TOWN_STATE) == 8){
        clearflag(FLAG_HIDE_LAB_RIVAL)
    }
    else{
        setflag(FLAG_HIDE_LAB_RIVAL)
    }
}
script Lab2_DexGet{
    applymovement(OBJ_EVENT_ID_PLAYER, Walk6Up)
    waitmovement(0)
    checkplayergender
    if(var(VAR_RESULT) == MALE){
        msgbox(Lab2_DexTalkMay)
    }
    if(var(VAR_RESULT) == FEMALE){
        msgbox(Lab2_DexTalkBrendan)
    }
    closemessage
    playfanfare(MUS_OBTAIN_TMHM)
    message("Obtained the Pokédex!")
    waitmessage
    waitfanfare
    setflag(FLAG_SYS_POKEDEX_GET)
	special(EnableNationalPokedex)
    setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)
	setvar(VAR_CABLE_CLUB_TUTORIAL_STATE, 1)
    setvar(VAR_OLDALE_TOWN_STATE, 1)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
    turnobject(5, DIR_WEST)
    msgbox("{RIVAL}: Here, you can have\nthese as well.")
    giveitem(ITEM_POKE_BALL, 5)
    msgbox("{RIVAL}: Now you can start catching\nyour own Pokémon and we can see which\lof us can fill our Pokédex faster!")
    checkplayergender
    if(var(VAR_RESULT) == MALE){
        msgbox("Last one out the door is a rotten egg!")
    }
    if(var(VAR_RESULT) == FEMALE){
        msgbox("Come on, let's go catch some Pokémon!")
    }
    closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, Lab2FaceDown)
	applymovement(5, Lab2_Movement_RivalExit1)
	waitmovement(0)
    delay(60)
	removeobject(5)
    setvar(VAR_PEARLWOOD_TOWN_STATE, 9)
    clearflag(FLAG_ROUTE1_STATE)
	savebgm(MUS_DUMMY)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
	fadedefaultbgm
    delay(25)
    msgbox("Prof. Birch: Sigh…\pSometimes I wonder\nwhere {RIVAL} gets all that energy…", MSGBOX_DEFAULT)
	release
}

movement Lab2_Movement_RivalExit1{
	player_run_down * 1
	player_run_left * 1
    player_run_down * 7
}

movement Lab2FaceDown{
	delay_16
	walk_in_place_faster_down
}

text Lab2_DexTalkMay{
    "Prof. Birch: {RIVAL}, {PLAYER},\nis there something you need?\p"
    "{RIVAL}: Oh boy, is there…\pYou gave {PLAYER} a Pokémon but\nnever gave him a Pokédex, dad!\pHow do you expect him to become\n a great trainer without one!?\p"
    "Prof. Birch: Oh, it must've\nslipped my mind…\p"
    "{RIVAL}: Slipped your mind!?\nYou're a Pokémon professor!\p"
    "Surely you realize that giving one to\n{PLAYER} helps your research as well?\p"
    "Prof. Birch: I know, I know.\nI'll get {PLAYER} one right away.\p"
    "Now, let's see…\l… … … …\pAh, darn it.\pLooks like I don't have any extra\nlying around at this time…\p"
    "{RIVAL}: Well we can't have {PLAYER}\nleaving empty-handed, now can we!?\p"
    "Prof. Birch: Here, {PLAYER},\nyou can have mine.\lI'll order myself a new one.\p"
    "{RIVAL}: Yay! Thanks, dad!\n"
}

text Lab2_DexTalkBrendan{
    "Prof. Birch: {RIVAL}, {PLAYER},\nis there something you need?\p"
    "{RIVAL}: As a matter of fact, there is.\nYou never gave {PLAYER} a Pokédex!\p"
    "How do you expect her to become\n a great trainer without one?\p"
    "Prof. Birch: Oh, it must've\nslipped my mind…\p"
    "{RIVAL}: Slipped your mind?\nAren't you a Pokémon professor?\p"
    "Surely giving one to {PLAYER}\nhelps your research as well.\p"
    "Prof. Birch: I know, I know.\nI'll get {PLAYER} one right away.\p"
    "Now, let's see…\l… … … …\pAh, darn it.\pLooks like I don't have any extra\nlying around at this time…\p"
    "{RIVAL}: Well we can't have {PLAYER}\nleaving empty-handed, now can we?\p"
    "Prof. Birch: Yes, I know. {PLAYER},\nyou can have mine.\lI'll order myself a new one.\p"
    "{RIVAL}: You're the best, dad!\n"
}


text Lab2_Intro_Speech{
    "Prof. Birch: Hello!\n"
    "You must be {PLAYER}!\p"
    "As you may know, it is customary in\n"
    "this town for every young person to\l"
    "be given their very first Pokémon.\p"
    "Some people use this opportunity to\n"
    "go on a journey of self-discovery\l"
    "while some are content with\l"
    "where they are now.\p"
    "What you do with your Pokémon is\n"
    "your decision."
}

text Lab2_PresentingBalls{
    "Over there on the table lies three\n"
    "Poké Balls. Each of them stores\l"
    "some very rare Pokémon.\p"
    "You may now go and choose one of them\n"
    "as your own.\p"
    "You may only choose one so make sure\n"
    "you decide carefully!"
}

script Lab2_Birch{
    lockall
    faceplayer
    if(var(VAR_PEARLWOOD_TOWN_STATE) == 13){
        call(ProfBirch_EventScript_RatePokedexOrRegister)
        end
    }
    if(var(VAR_PEARLWOOD_TOWN_STATE) == 12){
        if(flag(FLAG_RECEIVED_CAPSULE)){
            call(ProfBirch_EventScript_RatePokedexOrRegister)
            end
        }
        msgbox(YourFatherIsInTown, MSGBOX_NPC)
        end
    }
    if(var(VAR_PEARLWOOD_TOWN_STATE) == 11){
        msgbox(HeresYourStoneNow)
        closemessage
        checkitemspace(ITEM_DECIDUEITE)
        if(var(VAR_RESULT) == FALSE){
            msgbox(ComeGetYourStoneLaterShort, MSGBOX_NPC)
            end
        }
        else{
            switch(var(VAR_STARTER_MON)){
                case 0:
                    giveitem(ITEM_DECIDUEITE)
                case 1:
                    giveitem(ITEM_BLAZIKENITE)
                case 2:
                    giveitem(ITEM_EMPOLEONITE)
            }
            setvar(VAR_PEARLWOOD_TOWN_STATE, 12)
            waitse
            release
            end
        }
    }
    if(!flag(FLAG_SYS_POKEMON_GET)){
        msgbox("Prof. Birch: Go on, pick one of the\nballs from the table.")
    }
    else{
        msgbox("Prof. Birch: I hope you take\ngood care of your new companion.")
    }
    waitmessage
    release
    end
}

script Lab2_Ball1{
    setvar(VAR_TEMP_3, SPECIES_ROWLET)
    goto(Lab_BallMain)
}
script Lab2_Ball2{
    setvar(VAR_TEMP_3, SPECIES_TORCHIC)
    goto(Lab_BallMain)
}
script Lab2_Ball3{
    setvar(VAR_TEMP_3, SPECIES_PIPLUP)
    goto(Lab_BallMain)
}

script Lab_BallMain{
    if(flag(FLAG_SYS_POKEMON_GET)){
        msgbox("You already received a Pokémon.", MSGBOX_SIGN)
        end
    }
    lock
    showmonpic(VAR_TEMP_3, 10, 3)
    bufferspeciesname(STR_VAR_1, VAR_TEMP_3)
    msgbox("Do you want to choose {STR_VAR_1}?", MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        hidemonpic
        msgbox(Lab2_NotChosen)
        release
        end
    }
    givemon(VAR_TEMP_3, 5)
    playfanfare(MUS_OBTAIN_ITEM)
    message("{PLAYER} received the {STR_VAR_1}!")
    waitmessage
    waitfanfare
    setobjectxy(VAR_LAST_TALKED, 20, 0)
    setobjectxyperm(VAR_LAST_TALKED, 20, 0)
    applymovement(VAR_LAST_TALKED, Walk1Up)
    switch(var(VAR_TEMP_3)){
        case SPECIES_ROWLET:
            setvar(VAR_STARTER_MON, 0)
        case SPECIES_TORCHIC:
            setvar(VAR_STARTER_MON, 1)
        case SPECIES_PIPLUP:
            setvar(VAR_STARTER_MON, 2)
    }
    setflag(FLAG_SYS_POKEMON_GET)
    setflag(FLAG_ROUTE1_STATE)
    clearflag(FLAG_BROTHER_PEARLFIGHT)
    setvar(VAR_PEARLWOOD_TOWN_STATE, 5)
    msgbox("Do you want to give a nickname\nto your new Pokémon?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }
    release
}

text Lab2_NotChosen{
    "Take your time."
}

script Lab2_NoLeave{
    lock
    msgbox("{COLOR RED}{SHADOW LIGHT_RED}You haven't chosen a Pokémon yet!")
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Walk1Up)
    waitmovement(0)
    release
}
