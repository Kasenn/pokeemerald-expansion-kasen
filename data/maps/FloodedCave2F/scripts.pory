mapscripts FloodedCave2F_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: FC2F_Flags
    MAP_SCRIPT_ON_LOAD: FC2F_Flags
}

script FC2F_Flags{
    if(flag(FLAG_HIDE_STONE_SPHERE_FC)){
        setmetatile(6, 1, 0x219, TRUE)
        setmetatile(7, 1, 0x219, TRUE)
        setmetatile(8, 1, 0x219, TRUE)
        setmetatile(6, 2, 0x219, TRUE)
        setmetatile(7, 2, 0x219, TRUE)
        setmetatile(8, 2, 0x219, TRUE)
    }
    end
}

script FloodedCave2F_ActivatePedestal{
    if(var(VAR_FCCAVE) == 3){
        msgbox(FC_EmptyPedestal2, MSGBOX_NPC)
        end
    }
    lock
    msgbox(FC_EmptyPedestal, MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        closemessage
        release
        end
    }
    call(Common_OpenBag)
    bufferitemname(STR_VAR_1, VAR_ITEM_ID)
    if(var(VAR_ITEM_ID) == 0){
        closemessage
        release
        end
    }
    elif(var(VAR_ITEM_ID) == 805){
        clearflag(FLAG_HIDE_STONE_SPHERE_FC)
        setflag(FLAG_FC_DOOR_OPEN)
        removeitem(ITEM_STONE_SPHERE)
        removeobject(2)
        addobject(1)
        special(DrawWholeMapView)
        msgbox(FC_PutCorrectItem, MSGBOX_DEFAULT)
        closemessage
        setmetatile(6, 1, 0x20B, TRUE)
        setmetatile(7, 1, 0x20C, TRUE)
        setmetatile(8, 1, 0x20D, TRUE)
        setmetatile(6, 2, 0x213, TRUE)
        setmetatile(7, 2, 0x214, FALSE)
        setmetatile(8, 2, 0x215, TRUE)
        special(DrawWholeMapView)
        fadeoutbgm(0)
        delay(30)
        setvar(VAR_0x8004, 1)  // vertical pan
        setvar(VAR_0x8005, 1)  // horizontal pan
        setvar(VAR_0x8006, 8)  // num shakes
        setvar(VAR_0x8007, 5)  // shake delay
        special(ShakeCamera)
        waitstate
        delay(60)
        playse(SE_SWITCH)
        waitse
        delay(90)
        fadeinbgm(0)
        delay(30)
        release
    }
    else{
        msgbox(FC_PutWrongItem, MSGBOX_NPC)
        end
    }
}

script FloodedCave2F_TakeBackSphere{
    lock
    msgbox(FCTakeBackSphere, MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        closemessage
        release
        end
    }
    additem(ITEM_STONE_SPHERE)
    setflag(FLAG_HIDE_STONE_SPHERE_FC)
    clearflag(FLAG_FC_DOOR_OPEN)
    removeobject(1)
    addobject(2)
    special(DrawWholeMapView)
    msgbox(FC_TookOutSphere, MSGBOX_DEFAULT)
    closemessage
    setmetatile(6, 1, 0x219, TRUE)
    setmetatile(7, 1, 0x219, TRUE)
    setmetatile(8, 1, 0x219, TRUE)
    setmetatile(6, 2, 0x219, TRUE)
    setmetatile(7, 2, 0x219, TRUE)
    setmetatile(8, 2, 0x219, TRUE)
    special(DrawWholeMapView)
    fadeoutbgm(0)
    delay(30)
    setvar(VAR_0x8004, 1)  // vertical pan
    setvar(VAR_0x8005, 1)  // horizontal pan
    setvar(VAR_0x8006, 8)  // num shakes
    setvar(VAR_0x8007, 5)  // shake delay
    special(ShakeCamera)
    waitstate
    delay(60)
    playse(SE_SWITCH)
    waitse
    delay(90)
    fadeinbgm(0)
    delay(30)
    release
}

text FC_EmptyPedestal2{
    "There's an empty pedestal here."
}

text FC_EmptyPedestal{
    "There's an empty pedestal here.\p"
    "Would you like to try placing an item\n"
    "on it?"
}
text FC_PutCorrectItem{
    "{PLAYER} placed the {STR_VAR_1}\n"
    "on the pedestal.\p"
    "… {PAUSE_UNTIL_PRESS}… {PAUSE_UNTIL_PRESS}…"
}
text FC_PutWrongItem{
    "{PLAYER} placed the {STR_VAR_1}\n"
    "on the pedestal.\p"
    "… {PAUSE_UNTIL_PRESS}… {PAUSE_UNTIL_PRESS}…\p"
    "Nothing seems to be happening…\p"
    "{PLAYER} put the {STR_VAR_1}\n"
    "back in the bag."
}
text FCTakeBackSphere{
    "The Stone Sphere is resting on the\n"
    "pedestal.\p"
    "Would you like to pick it back up?"
}
text FC_TookOutSphere{
    "{PLAYER} took out the Stone Sphere\n"
    "from the table."
}