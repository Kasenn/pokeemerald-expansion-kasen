mapscripts Kaolisle_House1_MapScripts{}

script Kaolisle_Pianist{
    if(flag(FLAG_PIANOREWARD)){
        msgbox(ThankYouForListening2, MSGBOX_NPC)
        end
    }
    if(flag(FLAG_KAOLISLE_PLAYINGPIANO)){
        lockall
        special(CalcCyclingRoadChallenge)
        if(var(VAR_RESULT) <= 11){
            msgbox(BetterNotDisturb, MSGBOX_DEFAULT)
            closemessage
            release
            end
        }
        else{
            faceplayer
            msgbox(ThankYouForListening, MSGBOX_DEFAULT)
            checkitemspace(ITEM_HEART_SCALE, 2)
            if(var(VAR_RESULT) == FALSE){
                msgbox(NotEnoughRoom, MSGBOX_NPC)
                end
            }
            special(FinishCyclingRoadChallenge)
            giveitem(ITEM_HEART_SCALE, 2)
            clearflag(FLAG_KAOLISLE_PLAYINGPIANO)
            setflag(FLAG_PIANOREWARD)
            release
            end
        }
    }
    lockall
    faceplayer
    msgbox(ListenToMePlay, MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        msgbox(PianistSad, MSGBOX_NPC)
        end
    }
    else{
        turnobject(1, DIR_NORTH)
        fadeoutbgm(1)
        msgbox(PianistHereGoes, MSGBOX_DEFAULT)
        release
        special(Special_BeginCyclingRoadChallenge)
        playbgm(MUS_UNWAVERING, FALSE)
        setflag(FLAG_KAOLISLE_PLAYINGPIANO)
        end
    }
}

text ListenToMePlay{
    "I've been learning how to play\n"
    "the piano.\p"
    "One day I hope to play in front of a\n"
    "large crowd but right now I don't have\l"
    "the self-confidence to do that.\p"
    "I need to start with something smaller.\n"
    "Would you like to hear me play?"
}
text PianistSad{
    "Oh, okay…"
}
text PianistHereGoes{
    "Okay, here goes…"
}
text BetterNotDisturb{
    "Better not disturb her while she's\n"
    "playing…"
}
text ThankYouForListening{
    "Thank you for listening to my piano\n"
    "recital.\p"
    "I'm not sure I'm still ready to play in\n"
    "front of a crowd but this definitely\l"
    "gave me a huge confidence boost.\p"
    "As a thanks for staying till the end of\n"
    "the recital, I want you to have these."
}
text ThankYouForListening2{
    "Thanks again for the confidence boost."
}

script Kaolisle_Karatemaster{
    lock
    faceplayer
    if(flag(FLAG_RECEIVED_HM_ROCK_SMASH)){
        msgbox(RockSmashMonComesOut_Text, MSGBOX_NPC)
        end
    }
    msgbox(LostMemento, MSGBOX_DEFAULT)
    checkitem(ITEM_LOST_MEMENTO)
    if(var(VAR_RESULT) == TRUE){
        playse(SE_PIN)
	    applymovement(1, Common_Movement_ExclamationMark)
	    waitmovement(0)
	    delay(40)
        msgbox(FoundMemento, MSGBOX_DEFAULT)
        giveitem(ITEM_HM_ROCK_SMASH)
        setflag(FLAG_RECEIVED_HM_ROCK_SMASH)
        removeitem(ITEM_LOST_MEMENTO)
        msgbox(ExplainRockSmash, MSGBOX_DEFAULT)
    }
    release
    end
}

text LostMemento{
    "You look like a strong trainer.\p"
    "That means you must have been to\n"
    "numerous places.\p"
    "Tell me, have you come across any\n"
    "old pendants?\p"
    "A dear friend once gifted me a pendant\n"
    "but I seem to have misplaced it and\l"
    "have no idea where it could have gone…\p"
    "Please, if you find it,\n"
    "could you let me know?"
}
text FoundMemento{
    "That pendant…!\p"
    "Yes, that's the pendant I “lost”!\p"
    "I'm a retired karate master and\n"
    "you have just passed my test!\p"
    "Yes, I left that pendant at the end of\n"
    "the mines on purpose to see if anyone\l"
    "would be able to retrieve it for me.\p"
    "The mines are definitely no pushover,\n"
    "so seeing how you were able to return\l"
    "with the pendant tells me you were able\l"
    "to brave the dangers of that mine and\l"
    "return here safely.\p"
    "That means you and your Pokémon are\n"
    "worthy of my teachings!\l"
    "Here, have this Hidden Machine!"
}
text ExplainRockSmash{
    "That HM contains the move Rock Smash.\p"
    "If you come across large boulders\n"
    "that block your path…\p"
    "Well, use that HM move and smash\n"
    "them right out of your way!"
}
text RockSmashMonComesOut_Text{
    "Oh, yes, when you smash a rock,\n"
    "there's a chance a Pokémon could come\l"
    "flying out of hiding!\l"
    "Do make sure you're prepared!"
}

script Kaolisle_SphealHouseLady{
    faceplayer
    msgbox(Kaolisle_SphealHouseLady_Text, MSGBOX_DEFAULT)
    closemessage
    end
}
text Kaolisle_SphealHouseLady_Text{
    "Seeing the spheal playing with each\n"
    "other really soothes the soul.\p"
    "I have places to be but I dare not\n"
    "break up their play."
}

script Kaolisle_House4_Man{
    msgbox(Kaolisle_House4_Man_Text, MSGBOX_NPC)
    end
}
text Kaolisle_House4_Man_Text{
    "Kaolisle is known for its holiday resort\n"
    "as well as its mining operation.\p"
    "The mining is handled by Pokémon and\n"
    "there are zero machinery at work,\l"
    "which makes the mines practically\l"
    "pollution free, allowing the mines\l"
    "and the resort to co-exist peacefully."
}

script Kaolisle_House4_Woman{
    msgbox(Kaolisle_House4_Woman_Text, MSGBOX_NPC)
    end
}
text Kaolisle_House4_Woman_Text{
    "Have you noticed the white cliffs of\n"
    "Kaolisle City?\p"
    "There are certain minerals in the\n"
    "ground that give it its unique color."
}