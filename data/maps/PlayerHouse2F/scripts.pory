mapscripts PlayerHouse2F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: PlayerHouse2F_Start
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_PEARLWOOD_TOWN_STATE, 0: PlayerHouse2F_MomTalk
    ]
}

script PlayerHouse2F_Start{
    checkplayergender
    if(var(VAR_RESULT) == FEMALE){
        setmaplayoutindex(LAYOUT_PLAYER_HOUSE2F_MAY)
    }
}

script PlayerHouse2F_MomTalk{
    lock
    delay(60)
    setobjectxy(1, 7, 1)
    showobjectat(1, MAP_PLAYER_HOUSE2F)
    delay(60)
    applymovement(1, Walk1Down)
    waitmovement(0)
    turnobject(1, DIR_WEST)
    delay(10)
    msgbox(Player2FMomIntro)
    closemessage
    delay(30)
    applymovement(1, Walk1Up)
    waitmovement(0)
    removeobject(1)
    delay(30)
    applymovement(OBJ_EVENT_ID_PLAYER, Walk1Right)
    waitmovement(0)
    setflag(FLAG_PEARLWOOD_MOM_TALKED_TO)
    setvar(VAR_PEARLWOOD_TOWN_STATE, 1)
    setrespawn(HEAL_LOCATION_PLAYER_HOUSE2F)
    release
}

script PlayerHouse2F_ClockReminder{
    if(flag(FLAG_SET_WALL_CLOCK)){
        release
        end
    }
    lockall
    msgbox(PlayerHouse2F_ClockReminder_Text)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Walk1Left)
    waitmovement(0)
    release
}
text PlayerHouse2F_ClockReminder_Text{
    "Better set the clock before\n"
    "heading downstairs."
}

text Player2FMomIntro{
    "Mom: Rise and shine,\n"
    "{PLAYER}!\p"
    "Today's a very special day so\n"
    "when you're ready, come on\l"
    "downstairs.\p"
    "Oh but first, don't forget to\n"
    "set your new clock!"
}

script Player2FSetClock{
    checkplayergender
    if(var(VAR_RESULT) == MALE){
        setvar(VAR_0x8004, MALE)
    }
    else{
        setvar(VAR_0x8004, FEMALE)
    }
    goto_if_set (FLAG_SET_WALL_CLOCK, Players2FCheckClock)
	msgbox (PlayersHouse_2F_Text_ClockIsStopped, MSGBOX_DEFAULT)
    fadescreen (FADE_TO_BLACK)
	special (StartWallClock)
	waitstate
	setvar (VAR_PEARLWOOD_TOWN_STATE, 1)
	setflag (FLAG_SET_WALL_CLOCK)
	release
}

script Players2FCheckClock{
	incrementgamestat(GAME_STAT_CHECKED_CLOCK)
	fadescreen(FADE_TO_BLACK)
	special(Special_ViewWallClock)
	waitstate
	release
}