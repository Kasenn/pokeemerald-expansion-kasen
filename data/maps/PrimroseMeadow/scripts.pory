mapscripts PrimroseMeadow_MapScripts{
	MAP_SCRIPT_ON_TRANSITION: Common_EventScript_SetupRivalGfxId
}

script PrimroseMeadow_EventScript_Rival{
    lock
	msgbox(PrimroseMeadow_Text_RivalPokemon, MSGBOX_DEFAULT)
	closemessage
	checkplayergender
	if(var(VAR_RESULT) == MALE){
		playbgm(MUS_ENCOUNTER_BRENDAN, TRUE)
	}
	else{
		playbgm(MUS_ENCOUNTER_MAY, TRUE)
	}
	faceplayer
	applymovement(1, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(48)
	if(var(VAR_RESULT) == MALE){
		msgbox(PRMeadow_Text_MayLetsBattle)
		switch(var(VAR_STARTER_MON)){
			case 0:
				trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_TREECKO, PRMeadow_MayDefeated)
			case 1:
				trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_TORCHIC, PRMeadow_MayDefeated)
			case 2:
				trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_MUDKIP, PRMeadow_MayDefeated)
		}
		msgbox(PRMeadow_MayTimeToHeadBack, MSGBOX_DEFAULT)
	}
	else{
		msgbox(PRMeadow_Text_BrendanLetsBattle)
		switch(var(VAR_STARTER_MON)){
			case 0:
				trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_TREECKO, PRMeadow_BrendanDefeated)
			case 1:
				trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_TORCHIC, PRMeadow_BrendanDefeated)
			case 2:
				trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_MUDKIP, PRMeadow_BrendanDefeated)
		}
		msgbox(PRMeadow_BrendanTimeToHeadBack, MSGBOX_DEFAULT)
	}
	closemessage
	safefollow
	setvar(VAR_PEARLWOOD_TOWN_STATE, 8)
	switch(var(VAR_FACING)){
		case DIR_SOUTH:
			applymovement(1, PRMeadow_Movement_RivalExit1)
		case DIR_NORTH:
			applymovement(OBJ_EVENT_ID_PLAYER, PRMeadow_Movement_WatchRivalExitFacingNorth)
			getfollowerxy(VAR_TEMP_0, VAR_TEMP_1)
			if(var(VAR_TEMP_0) == 9 && var(VAR_TEMP_1) == 9){
				applymovement(1, PRMeadow_Movement_RivalExitFacingNorth2)
			}
			else{
				applymovement(1, PRMeadow_Movement_RivalExitFacingNorth1)
			}
		case DIR_EAST:
		case DIR_WEST:
			applymovement(OBJ_EVENT_ID_PLAYER, PRMeadow_Movement_WatchRivalExitFacingEastOrWest)
			applymovement(1, PRMeadow_Movement_RivalExit1)
	}
	waitmovement(0)
	removeobject(1)
	savebgm(MUS_DUMMY)
	fadedefaultbgm
	release
	end
}

movement PRMeadow_Movement_RivalExitFacingNorth1{
	player_run_right
	player_run_down * 11
	player_run_left * 3
}

movement PRMeadow_Movement_RivalExitFacingNorth2{
	player_run_right * 2
	player_run_down * 11
	player_run_left * 3
}

movement PRMeadow_Movement_WatchRivalExitFacingNorth{
	delay_16
	delay_4
	walk_in_place_faster_right
	delay_16
	walk_in_place_faster_down
}

movement PRMeadow_Movement_RivalExit1{
	player_run_down * 11
	player_run_left * 3
}

movement PRMeadow_Movement_WatchRivalExitFacingEastOrWest{
	delay_16
	walk_in_place_faster_down
}

text PrimroseMeadow_Text_RivalPokemon{
    "{RIVAL}: Let's see… The Pokémon found\n"
	"in Primrose Meadow include…"
}

text PRMeadow_Text_MayLetsBattle{
    "Oh, hi {PLAYER}!\p"
	"I knew it!\n"
	"My dad did give you a Pokémon!\p"
	"Since we're here, let's have a quick\n"
	"battle!\p"
	"I'll give you a taste of what being\n"
	"a trainer is like."
}

text PRMeadow_MayDefeated{
    "Wow! That's great!\n"
	"{PLAYER}, you're pretty good!"
}

text PRMeadow_Text_BrendanLetsBattle{
    "Hey, it's {PLAYER}!\p"
	"…Oh yeah, I knew dad was going\n"
	"to give you a Pokémon.\p"
	"Since we're here, how about a little\n"
	"battle?\p"
	"I'll teach you what being a trainer's\n"
	"about!"
}

text PRMeadow_BrendanDefeated{
    "Huh, {PLAYER}, that wasn't too bad."
}

text PRMeadow_MayTimeToHeadBack{
    "{RIVAL}: You really are something,\n"
	"{PLAYER}!\p"
	"Keep filling that Pokédex and training\n"
	"your Pokémon and I'm sure you'll\l"
	"be able to take on the Pokémon League\l"
	"in no time!\p"
	"…What do you mean, “What Pokédex?”\p"
	"…WHAT!?\n"
	"My dad didn't give you one!?\p"
	"Oh I'll make sure he hears about this.\n"
	"Come on, let's get back to the lab!"
}

text PRMeadow_BrendanTimeToHeadBack{
    "{RIVAL}: That was a good battle,\n"
	"{PLAYER}!\p"
	"Keep filling that Pokédex and training\n"
	"your Pokémon and I'm sure you'll\l"
	"be able to take on the Pokémon League\l"
	"in no time!\p"
	"…What do you mean, “What Pokédex?”\p"
	"…What?\n"
	"My dad didn't give you one?\p"
	"Oh we need to fix this.\n"
	"Come on, let's get back to the lab!"
}