mapscripts Sandstone_SmallHouse1_MapScripts{}

script Sandstone_SmallHouse_Woman{
    msgbox (Sandstone_SmallHouse_Woman_Text, MSGBOX_NPC)
    end
}
script Sandstone_SmallHouse_Child{
    if(flag(FLAG_SANDSTONE_BALLOON)){
        msgbox(DontLetItPop, MSGBOX_NPC)
        end
    }
    lock
    faceplayer
    msgbox(Sandstone_SmallHouse_Child_TakeThis, MSGBOX_DEFAULT)
    checkitemspace(ITEM_AIR_BALLOON)
    if(var(VAR_RESULT) == FALSE){
        msgbox(NotEnoughRoom, MSGBOX_NPC)
        end
    }
    giveitem(ITEM_AIR_BALLOON)
    waitfanfare
    closemessage
    setflag(FLAG_SANDSTONE_BALLOON)
    release
    end
}
text Sandstone_SmallHouse_Woman_Text{
    "Good thing I managed to grab that doll\n"
    "just before the department store ran\l"
    "out of stock.\l"
    "My daughter really seems to love it."
}
text Sandstone_SmallHouse_Child_TakeThis{
    "I got a new doll!\p"
    "Here, I don't need this anymore.\n"
    "You can have it!"
}
text DontLetItPop{
    "Don't let it pop!"
}
script Sandstone_SmallHouse_Man1{
    msgbox (Sandstone_SmallHouse_Man1_Text, MSGBOX_NPC)
    end
}
script Sandstone_SmallHouse_Woman1{
    msgbox (Sandstone_SmallHouse_Woman1_Text, MSGBOX_NPC)
    end
}
text Sandstone_SmallHouse_Man1_Text{
    "Our living expenses have risen so much\n"
    "ever since moving to a big city.\p"
    "Thankfully our salaries are also\n"
    "higher."
}
text Sandstone_SmallHouse_Woman1_Text{
    "I wasn't sure if living in a big city\n"
    "would be for me but now that we've\l"
    "lived here for a while, I don't think\l"
    "I could go back."
}

script Sandstone_SmallHouse_OldMan{
    msgbox(Sandstone_SmallHouse_OldMan_Text, MSGBOX_NPC)
    end
}
text Sandstone_SmallHouse_OldMan_Text{
    "The city has tried buying my house\n"
    "countless of times just so that they\l"
    "can demolish it and replace it with\l"
    "yet another soulless high-rise.\p"
    "I tell you, for as long as I'm still\n"
    "breathing, I will never sell!"
}

script Sandstone_SmallHouse_FisherWoman{
    if(flag(FLAG_RECEIVED_GOOD_ROD)){
        msgbox(HowIsClancy, MSGBOX_NPC)
        end
    }
    lock
    faceplayer
    checkitem(ITEM_LURE)
    if(var(VAR_RESULT) == FALSE){
        msgbox(Sandstone_LookingForFisher, MSGBOX_DEFAULT)
        closemessage
    }
    if(flag(FLAG_QUEST_FOR_GOOD_ROD)){
        checkitem(ITEM_LURE)
        if(var(VAR_RESULT) == TRUE){
            msgbox(TakeToClancy, MSGBOX_NPC)
            end
        }
        playse(SE_PIN)
        applymovement(1, Common_Movement_QuestionMark)
        waitmovement(0)
        delay(30)
        msgbox(YouMetClancy, MSGBOX_DEFAULT)
        giveitem(ITEM_LURE)
        waitfanfare
        msgbox(TakeToClancy, MSGBOX_NPC)
        end
    }
}

text Sandstone_LookingForFisher{
    "Hello. Are you looking for the\n"
    "Fishing Guru?\p"
    "That would be my husband.\p"
    "He can never sit still for long and is\n"
    "probably on a beach somewhere, fishing."
}
text TakeToClancy{
    "Please bring that to Clancy,\n"
    "wherever he currently is."
}
text YouMetClancy{
    "You met Clancy and he asked if you\n"
    "could get him his lure?\p"
    "I wish he would've come to get it\n"
    "himself instead of sending other people\l"
    "on errands but since you're here, you\l"
    "may as well take this."
}
text HowIsClancy{
    "I hope Clancy was doign well.\p"
    "He sometimes gets so fixated on fishing\n"
    "that he even forgets to eat!"
}

script WardrobeLeft{
    call(WardrobeMain)
    opendoor(5, 2)
    call(WardrobeDoor)
    closedoor(5, 2)
    waitdooranim
    warpsilent(MAP_LILYCOVE_CITY_DEPARTMENT_STORE_5F, 5, 2)
    waitstate
    end
}
script WardrobeRight{
    call(WardrobeMain)
    opendoor(7, 2)
    call(WardrobeDoor)
    closedoor(7, 2)
    waitdooranim
    warpsilent(MAP_LILYCOVE_CITY_DEPARTMENT_STORE_5F, 7, 2)
    waitstate
    end
}

script WardrobeMain{
    if(!flag(FLAG_PURCHASED_OUTFIT)){
        msgbox(ItsaChangingRoom, MSGBOX_SIGN)
        end
    }
    lock
    msgbox(ChangeOutfit, MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        closemessage
        release
        end
    }
    closemessage
    if(flag(FLAG_PC_CHANGE_COSTUME)){
        clearflag(FLAG_PC_CHANGE_COSTUME)
    }
    else{
        setflag(FLAG_PC_CHANGE_COSTUME)
    }
    return
}

text ItsaChangingRoom{
    "It's a dressing room for trying on and\n"
    "changing clothes."
}

text ChangeOutfit{
    "It's a dressing room for trying on and\n"
    "changing clothes.\p"
    "Would you like to change your outfit?"
}

script WardrobeDoor{
    waitdooranim
    applymovement(OBJ_EVENT_ID_PLAYER, WardrobeUp)
    waitmovement(0)
    return
}

movement WardrobeUp{
    walk_up
    set_invisible
}

script LilycoveCity_Clothier{
    if(flag(FLAG_PURCHASED_OUTFIT)){
        msgbox(UseRoomToChange, MSGBOX_NPC)
        end
    }
    lock
    faceplayer
    msgbox(InNeedOfOutfit, MSGBOX_DEFAULT)
    goto(ClothierMain)
}

script ClothierMain{
    message("What would you like to do?")
    waitmessage
    multichoice(23, 0, MULTI_OUTFIT, 0)
    switch(var(VAR_RESULT)){
        case 0:
            goto(PurchaseOutfit)
        case 1:
            goto(PreviewOutfit)
        case 2:
            goto(ThanksForShopping)
        case MULTI_B_PRESSED:
            goto(ThanksForShopping)
    }
}

script PreviewOutfit{
    checkplayergender
    if(var(VAR_RESULT) == MALE){
        showmonpic(SPECIES_KRABBY, 10, 3)
    }
    else{
        showmonpic(SPECIES_KINGLER, 10, 3)
    }
    msgbox(LooksGoodOnYou, MSGBOX_DEFAULT)
    closemessage
    hidemonpic
    goto(ClothierMain)
}

script ThanksForShopping{
    hidemoneybox
    msgbox(gText_PleaseComeAgain, MSGBOX_DEFAULT)
    closemessage
	release
	end
}

script PurchaseOutfit{
    showmoneybox(0, 0)
    msgbox(TotalIs25k, MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        goto(ThanksForShopping)
    }
    checkmoney(25000)
    if(var(VAR_RESULT) == FALSE){
        msgbox(NotEnoughMoney, MSGBOX_NPC)
        hidemoneybox
        end
    }
    playse(SE_SHOP)
    removemoney(25000)
    updatemoneybox
    msgbox(UseDressingRoom, MSGBOX_NPC)
    hidemoneybox
    setflag(FLAG_PURCHASED_OUTFIT)
    end
}

text InNeedOfOutfit{
    "Welcome!\p"
    "This is the Sandstone Department Store\n"
    "clothing section.\p"
    "We are currently having a huge sale on\n"
    "clothing and clothing apparel."
}
text LooksGoodOnYou{
    "Ooh!\p"
    "You look absolutely dashing in that\n"
    "outfit! Wouldn't you agree?"
}
text TotalIs25k{
    "With the clothing and the apparel, your\n"
    "total comes to ¥25,000.\p"
    "Would you like to purchase a new\n"
    "outfit?"
}
text NotEnoughMoney{
    "Oh, I'm sorry, but you can't afford\n"
    "that outfit…"
}
text UseDressingRoom{
    "Thank you for your purchase!\p"
    "You can switch between your outfits by\n"
    "accessing the dressing rooms located\l"
    "right next to the elevator.\p"
    "Please come again!"
}
text UseRoomToChange{
    "You can switch between your outfits by\n"
    "accessing the dressing rooms located\l"
    "right next to the elevator.\p"
    "Please come again!"
}