mapscripts RanchHouse_MapScripts{}

script RanchHouseMan{
    if(flag(FLAG_OBTAINED_MEGA_RING)){
        if(flag(FLAG_OBTAINED_AMPHAROSITE)){
            msgbox(RanchTakeGoodCare, MSGBOX_NPC)
            end
        }
        lock
        faceplayer
        msgbox(RanchHouseItsYou, MSGBOX_NPC)
        checkitemspace(ITEM_AMPHAROSITE)
        if(var(VAR_RESULT) == FALSE){
            msgbox(NotEnoughRoom, MSGBOX_NPC)
            end
        }
        giveitem(ITEM_AMPHAROSITE)
        waitfanfare
        setflag(FLAG_OBTAINED_AMPHAROSITE)
        closemessage
        release
        end
    }
    msgbox(RanchHouseManText, MSGBOX_NPC)
    end
}

text RanchHouseItsYou{
    "Ah, it's you!\p"
    "One of our Mareeps found this peculiar\n"
    "stone the other day.\p"
    "I have no idea what it does, but I\n"
    "never got to thank you before,\l"
    "so I hope this stone will suffice."
}
text RanchTakeGoodCare{
    "Take good care now!"
}

script RanchHouseWoman{
    msgbox(RanchHouseWomanText, MSGBOX_NPC)
    end
}

text RanchHouseManText{
    "You must be the trainer who was helping\n"
    "our farm alongside Jasmine and Volkner.\l"
    "Thank you so much!\p"
    "I wish I had something to offer\n"
    "in return but it'll take us a while to\l"
    "recover from these troubles that\l"
    "have been plaguing us recentlyâ€¦"
}

text RanchHouseWomanText{
    "Our farm has gone through\n"
    "a lot recently.\p"
    "Still, I'm sure the future\n"
    "will be bright."
}