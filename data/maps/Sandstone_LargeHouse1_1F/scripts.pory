mapscripts Sandstone_LargeHouse1_1F_MapScripts{}

script Sandstone_BigHouse1_2F_Child1{
    lock
    msgbox(Sandstone_BigHouse1_2F_Child1_Text)
    release
    end
}
script Sandstone_BigHouse1_2F_Child2{
    lock
    msgbox(Sandstone_BigHouse1_2F_Child2_Text)
    release
    end
}
script Sandstone_BigHouse1_2F_Mother{
    msgbox(Sandstone_BigHouse1_2F_Mother_Text, MSGBOX_NPC)
    end
}
script Sandstone_BigHouse1_1F_Pokefan{
    msgbox(Sandstone_BigHouse1_1F_Pokefan_Text, MSGBOX_NPC)
    end
}
script Sandstone_BigHouse1_1F_OldMan{
    msgbox(Sandstone_BigHouse1_1F_OldMan_Text, MSGBOX_NPC)
    end
}
script Sandstone_BigHouse1_1F_OldWoman{
    if(flag(FLAG_SANDSTONE_LAVACOOKIE)){
        msgbox(Sandstone_BigHouse1_1F_OldWoman_Text, MSGBOX_NPC)
        end
    }
    lock
    faceplayer
    msgbox(HaveACookie)
    checkitemspace(ITEM_LAVA_COOKIE)
    if(var(VAR_RESULT) == FALSE){
        msgbox(NotEnoughRoom, MSGBOX_NPC)
        end
    }
    giveitem(ITEM_LAVA_COOKIE)
    waitfanfare
    setflag(FLAG_SANDSTONE_LAVACOOKIE)
    release
    closemessage
    end
}

text Sandstone_BigHouse1_2F_Child1_Text{
    "Swablu! Use Fly!"
}
text Sandstone_BigHouse1_2F_Child2_Text{
    "Azurill! Bubble, now!"
}
text Sandstone_BigHouse1_2F_Mother_Text{
    "Those two are so passionate in their\n"
    "play, I bet they'll become amazing\l"
    "trainers when they grow up."
}
text Sandstone_BigHouse1_1F_Pokefan_Text{
    "Back in my youth I yearned to become\n"
    "the greatest Pokémon trainer there was.\p"
    "I never managed to defeat a single\n"
    "Gym Leader, so I decided to become\l"
    "a family man instead."
}
text Sandstone_BigHouse1_1F_OldMan_Text{
    "You should go visit your family once\n"
    "in a while.\p"
    "Adventures can be fun, but I'm sure\n"
    "your parents must miss you."
}
text Sandstone_BigHouse1_1F_OldWoman_Text{
    "Stay safe on your travels."
}
text HaveACookie{
    "Well aren't you a cutie. Here,\n"
    "have a cookie."
}

script Sandstone_BigHouse1_1F_Youngster{
    lockall
    faceplayer
    if(flag(FLAG_TRADED_SMEARGLE)){
        msgbox(SmeargleTraded, MSGBOX_NPC)
	    end
    }
    setvar(VAR_0x8008, INGAME_TRADE_SMEARGLE)
    copyvar(VAR_0x8004, VAR_0x8008)
    specialvar(VAR_0x8009, GetInGameTradeSpeciesInfo)
    msgbox(WantToTradeSmeargle, MSGBOX_YESNO)
    goto_if_eq(VAR_RESULT, NO, SmeargleTradeDecline)
    special(ChoosePartyMon)
	waitstate
	goto_if_eq(VAR_0x8004, PARTY_NOTHING_CHOSEN, SmeargleTradeDecline)
	copyvar(VAR_0x8005, VAR_0x8004)
	specialvar(VAR_RESULT, GetTradeSpecies)
	goto_if_ne(VAR_RESULT, VAR_0x8009, SmeargleNotRequestedMon)
	copyvar(VAR_0x8004, VAR_0x8008)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate
    msgbox(SmeargleTradeThanks, MSGBOX_DEFAULT)
	setflag(FLAG_TRADED_SMEARGLE)
	release
	end
}

script SmeargleTradeDecline{
    msgbox(SmeargleTradeDeclineText, MSGBOX_NPC)
	end
}

script SmeargleNotRequestedMon{
	bufferspeciesname(STR_VAR_1, VAR_0x8009)
	msgbox(SmeargleWrongMonText, MSGBOX_NPC)
	end
}

text SmeargleTraded{
    "This Rockruff is even cooler from\n"
    "up close!"
}
text SmeargleTradeThanks{
    "Oh cool! I finally got a Rockruff!\n"
    "Thank you so much!"
}
text SmeargleTradeDeclineText{
    "Let me know if you change your mind."
}
text WantToTradeSmeargle{
    "I'm so bored!\p"
    "All the adults do nothing but talk with\n"
    "each other and my younger siblings are\l"
    "no fun either.\p"
    "Do you want to like, trade Pokémon or\n"
    "something?\p"
    "I've got this cool Pokémon called\n"
    "Smeargle. Maybe trade it to something\l"
    "like, oh I don't know, a Rockruff?"
}
text SmeargleWrongMonText{
    "I don't know what kind of game you're\n"
    "trying to play, but that's clearly not\l"
    "a {STR_VAR_1}."
}

script Aldeleaf_Artist{
    if(flag(FLAG_OBTAINED_PAINTBRUSH)){
        msgbox(ArtistAfterword, MSGBOX_NPC)
        end
    }
    lock
    faceplayer
    if(flag(FLAG_TEMP_2)){
        goto(GivePaintBrush)
    }
    msgbox(Aldeleaf_Artist_Text)
    checkpartymon(SPECIES_SMEARGLE, 0)
    if(var(VAR_RESULT) == FALSE){
        release
        end
    }
    playse(SE_PIN)
    applymovement(4, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(4, Common_Movement_Delay48)
	waitmovement(0)
    msgbox(YouHaveSmeargle)
    checkitemspace(ITEM_PAINTBRUSH)
    if(var(VAR_RESULT) == FALSE){
        msgbox(NotEnoughRoom, MSGBOX_NPC)
        setflag(FLAG_TEMP_2)
        end
    }
    goto(GivePaintBrush)
}
script GivePaintBrush{
    giveitem(ITEM_PAINTBRUSH)
    waitfanfare
    msgbox(GiveToSmeargle, MSGBOX_NPC)
    setflag(FLAG_OBTAINED_PAINTBRUSH)
    end
}

text Aldeleaf_Artist_Text{
    "Among artists like myself, this museum\n"
	"is an inspiration.\p"
	"I wonder, are there any Pokémon that\n"
	"could be seen as artists?\l"
	"I would very much like to see one."
}
text GiveToSmeargle{
    "If a Smeargle holds that Paintbrush,\n"
    "both of its Attack stats will increase\l"
    "massively, allowing its works of art to\l"
    "really make an impact."
}
text YouHaveSmeargle{
    "Why that Smeargle of yours is a\n"
    "natural-born artist.\p"
    "Here, try giving it this."
}
text ArtistAfterword{
    "A painter is nothing without their\n"
    "tools.\p"
    "Give that Paintbrush to a Smeargle\n"
    "and witness the impact of makes on\l"
    "its art."
}
	