mapscripts Kaolisle_Hotel_1F_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: HideJasmine
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_KAOLISLE_HOTEL_STATE, 1: JasmineLeavesHotel
    ]
}

script JasmineLeavesHotel{
    lock
    delay(30)
    turnobject(24, DIR_SOUTH)
    playse(SE_PIN)
	applymovement(24, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(70)
    applymovement(24, HotelWalkDown2)
    applymovement(25, HotelWalkDown2)
    waitmovement(0)
    delay(10)
    if(flag(FLAG_TALKED_TO_JASMINE_AT_HOTEL)){
        msgbox(Hotel1F_JasmineGoodbye, MSGBOX_DEFAULT)
        closemessage
        playmoncry(SPECIES_AMPHAROS, CRY_MODE_NORMAL)
        delay(30)
        turnobject(24, DIR_NORTH)
        waitmoncry
        delay(60)
        turnobject(24, DIR_SOUTH)
        delay(30)
        msgbox(AmphyAlso, MSGBOX_DEFAULT)
    }
    else{
        msgbox(Hotel1F_LongTimeNoSeeGoodbye, MSGBOX_DEFAULT)
        closemessage
        playmoncry(SPECIES_AMPHAROS, CRY_MODE_NORMAL)
        delay(30)
        turnobject(24, DIR_NORTH)
        waitmoncry
        delay(60)
        turnobject(24, DIR_SOUTH)
        delay(30)
        msgbox(MyCompanionAmphyAlso, MSGBOX_DEFAULT)
    }
    waitmessage
    closemessage
    delay(20)
    applymovement(24, HotelWalkLeft1)
    applymovement(25, HotelWalkDown1)
    waitmovement(0)
    applymovement(24, HotelWalkDown1)
    applymovement(25, HotelWalkLeft1)
    waitmovement(0)
    removeobject(24)
    applymovement(25, HotelWalkDown1)
    waitmovement(0)
    removeobject(25)
    setflag(FLAG_HIDEJASMINE_HOTEL)
    delay(20)
    setvar(VAR_KAOLISLE_HOTEL_STATE, 2)
    release
    end
}
text Hotel1F_JasmineGoodbye{
    "Jasmine: Oh, hello again, {PLAYER}.\p"
    "I um… just finished checking out, so\n"
    "we were about to start heading home…\p"
    "…Oh?\p"
    "It seems you already obtained your\n"
    "3rd Gym Badge.\p"
    "Er, let's see… If I'm not mistaken,\n"
    "your next gym challenge should be\l"
    "the one in Sandstone City…\p"
    "I've always found that gym to be\n"
    "a bit… weird… but, um… I don't know\l"
    "how to say this, but good luck…"
}
text AmphyAlso{
    "Jasmine: …Oh, I think Amphy is also\n"
    "trying to cheer you on!\p"
    "…{PAUSE 0x0F}{PAUSE 0x0F}{PAUSE 0x0F}{PAUSE 0x0F} …{PAUSE 0x0F}{PAUSE 0x0F}{PAUSE 0x0F}{PAUSE 0x0F} …{PAUSE 0x0F}{PAUSE 0x0F}{PAUSE 0x0F}{PAUSE 0x0F}\n"
    "A-anyway, we should be heading out…\p"
    "I fear to even imagine the amount of\n"
    "work that has accumulated during our\l"
    "time away…\p"
    "Um… It was nice seeing you again…"
}
text Hotel1F_LongTimeNoSeeGoodbye{
    "Jasmine: Oh, hello.\n"
    "Um… {PLAYER}… was it?\p"
    "I was on my way back home from the\n"
    "ranch, but, um… the trip was so long\l"
    "that I thought it best to have a rest\l"
    "at this hotel…\p"
    "Y-you look like you've grown a lot\n"
    "as a trainer since we last met.\p"
    "I hope your journey hasn't been\n"
    "too arduous…\p"
    "…Oh?\p"
    "It seems you already obtained your\n"
    "3rd Gym Badge.\p"
    "Er, let's see… If I'm not mistaken,\n"
    "your next gym challenge should be\l"
    "the one in Sandstone City…\p"
    "I've always found that gym to be\n"
    "a bit… weird… but, um… I don't know\l"
    "how to say this, but good luck…"
}
text MyCompanionAmphyAlso{
    "Jasmine: …Oh, this big fella…\n"
    "She is my traveling companion, Amphy.\p"
    "I believe she's also trying to\n"
    "cheer you on!\p"
    "…{PAUSE 0x0F}{PAUSE 0x0F}{PAUSE 0x0F}{PAUSE 0x0F} …{PAUSE 0x0F}{PAUSE 0x0F}{PAUSE 0x0F}{PAUSE 0x0F} …{PAUSE 0x0F}{PAUSE 0x0F}{PAUSE 0x0F}{PAUSE 0x0F}\n"
    "A-anyway, we should be heading out…\p"
    "I fear to even imagine the amount of\n"
    "work that has accumulated during our\l"
    "time away…\p"
    "Um… It was nice seeing you again…"
}
script HideJasmine{
    setvar(VAR_TEMP_5, 0)
    if(!flag(FLAG_BADGE03_GET)){
        setflag(FLAG_HIDEJASMINE_HOTEL)
    }
    end
}
movement HotelWalkDown2{
    walk_down * 2
}
movement HotelWalkLeft1{
    walk_left
}
movement HotelWalkDown1{
    walk_down
}


text BuffetTr1_Intro{
    "Please don't look at me…\n"
    "Let's just…{PAUSE_UNTIL_PRESS} get this out of the way…"
}
text BuffetTr1_Lose{
    "… … … …"
}
text BuffetTr1_PostBattle{
    "…Are we done?"
}
////
text BuffetTr2_Intro{
    "Hope you're thirsty, because my\n"
    "Water-type Pokémon will wash over you\l"
    "like a tsunami!"
}
text BuffetTr2_Lose{
    "My precious Pokémon!"
}
text BuffetTr2_PostBattle{
    "Only reason I lost was due to terrain\n"
    "disadvantage.\p"
    "In water, my Pokémon are invincible!"
}
////
text BuffetTr3_Intro{
    "Hope you're hungry for a beating\n"
    "because I won't be holding back!"
}
text BuffetTr3_Lose{
    "Agh! I spilled my soup!"
}
text BuffetTr3_PostBattle{
    "Food tastes better when shared!\n"
    "Enjoy the soup!"
}
////
text BuffetTr4_Intro{
    "Alola! I traveled from far to get\n"
    "here, so I hope you can satisfy\l"
    "my expectations!"
}
text BuffetTr4_Lose{
    "Well, you certainly didn't\n"
    "disappoint…"
}
text BuffetTr4_PostBattle{
    "When I first arrived here, I was a bit\n"
    "crestfallen when I couldn't find any\l"
    "place that sells Malasadas.\p"
    "Thinking back, maybe it was\n"
    "for the best.\p"
    "Had I gone for the safe and familiar\n"
    "option, I might have completely missed\l"
    "out on all the local specialties!"
}

text HaveSomeSoup{
    "You really shook things up!\n"
    "How about we celebrate with a bowl\l"
    "of special “Berry Blend Soup”?\l"
    "Your Pokémon will love it!"
}

text EatMeal{
    "Would you like to enjoy a meal with\n"
    "your Pokémon, healing the entire team?\p"
    "This can be done only once per attempt."
}
script EatDeliciousMeal{
    call(Common_EventScript_OutOfCenterPartyHeal)
    setflag(FLAG_TEMP_4)
    delay(30)
    return
}
script BuffetOfferSoup{
    if(flag(FLAG_RECEIVED_SOUP)){
        return
    }
    msgbox(HaveSomeSoup, MSGBOX_DEFAULT)
    giveitem(ITEM_SPECIAL_SOUP)
    waitse
    waitmessage
    setflag(FLAG_RECEIVED_SOUP)
    return
}

script Hotel_BuffetTrigger{
    lockall
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    delay(20)
    if((defeated(TRAINER_BUFFET_1))
    &&(defeated(TRAINER_BUFFET_2))
    &&(defeated(TRAINER_BUFFET_3))
    &&(defeated(TRAINER_BUFFET_4))
    &&(!flag(FLAG_BUFFET_COMPLETE))){
        goto(BuffetComplete)
    }
    if(flag(FLAG_BUFFET_COMPLETE)){
        goto(BuffetAlreadyCompleted)
    }
    msgbox(BuffetIntro, MSGBOX_DEFAULT)
    goto(BuffetMultiChoice)
    end
}
script BuffetMultiChoice{
    showmoneybox(0, 0)
    message(BuffetWhatWouldYouHave)
    waitmessage
    multichoice(23, 0, MULTI_BUFFET, 0)
    switch(var(VAR_RESULT)){
        case 0:
            msgbox(ThatWillBe1000, MSGBOX_YESNO)
            if(var(VAR_RESULT) == FALSE){
                goto(BuffetComeBackLater)
            }
            goto(BuffetDineHere)
        case 1:
            goto(ExplainBuffetRules)
        case 2:
            goto(BuffetComeBackLater)
        case MULTI_B_PRESSED:
            goto(BuffetComeBackLater)
    }
    end
}
script BuffetDineHere{
    checkmoney(1000)
    if(var(VAR_RESULT) == FALSE){
        goto(BuffetNotEnoughMoney)
    }
    waitse
    removemoney(1000)
    playse(SE_SHOP)
    updatemoneybox
    waitse
    msgbox(EnjoyYourBuffet, MSGBOX_DEFAULT)
    closemessage
    hidemoneybox
    cleartrainerflag(TRAINER_BUFFET_1)
    cleartrainerflag(TRAINER_BUFFET_2)
    cleartrainerflag(TRAINER_BUFFET_3)
    cleartrainerflag(TRAINER_BUFFET_4)
    setflag(FLAG_SYSTEM_NOREWARDBATTLES)
    goto(WalkToFirstTable)
}
script WalkToFirstTable{
    applymovement(OBJ_EVENT_ID_PLAYER, WalkTo1stTable)
    waitmovement(0)
    msgbox(BuffetTr1_Intro)
    trainerbattle_no_intro(TRAINER_BUFFET_1, BuffetTr1_Lose)
    goto(WalkToSecondTable)
}
script WalkToSecondTable{
    msgbox(BuffetTr1_PostBattle)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, WalkTo2ndTable)
    waitmovement(0)
    msgbox(EatMeal, MSGBOX_YESNO)
    closemessage
    if(var(VAR_RESULT) == TRUE){
        call(EatDeliciousMeal)
    }
    msgbox(BuffetTr2_Intro)
    trainerbattle_no_intro(TRAINER_BUFFET_2, BuffetTr2_Lose)
    goto(WalkToThirdTable)
}
script WalkToThirdTable{
    msgbox(BuffetTr2_PostBattle)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, WalkTo3rdTable)
    waitmovement(0)
    if(!flag(FLAG_TEMP_4)){
        msgbox(EatMeal, MSGBOX_YESNO)
        closemessage
        if(var(VAR_RESULT) == TRUE){
            call(EatDeliciousMeal)
        }
    }
    msgbox(BuffetTr3_Intro)
    trainerbattle_no_intro(TRAINER_BUFFET_3, BuffetTr3_Lose)
    goto(WalkToFourthTable)
}
script WalkToFourthTable{
    call(BuffetOfferSoup)
    msgbox(BuffetTr3_PostBattle)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, WalkTo2ndTable)
    waitmovement(0)
    if(!flag(FLAG_TEMP_4)){
        msgbox(EatMeal, MSGBOX_YESNO)
        closemessage
        if(var(VAR_RESULT) == TRUE){
            call(EatDeliciousMeal)
        }
    }
    msgbox(BuffetTr4_Intro)
    trainerbattle_no_intro(TRAINER_BUFFET_4, BuffetTr4_Lose)
    goto(BuffetFinale)
}
script BuffetFinale{
    fadescreen(FADE_TO_BLACK)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    setobjectxy(OBJ_EVENT_ID_PLAYER, 13, 7)
    delay(30)
    fadescreen(FADE_FROM_BLACK)
    delay(60)
    goto(BuffetComplete)
}

movement WalkTo1stTable{
    walk_left
    walk_up
    walk_left * 5
    walk_down
    walk_in_place_faster_right
    step_end
}
movement WalkTo2ndTable{
    walk_down * 3
    walk_left * 2
    step_end
}
movement WalkTo3rdTable{
    walk_down * 2
    walk_right * 2
    step_end
}

script BuffetComplete{
    msgbox(BuffetCompleted, MSGBOX_DEFAULT)
    checkitemspace(ITEM_LEFTOVERS)
    if(var(VAR_RESULT) == FALSE){
        msgbox(NotEnoughRoomLeftovers, MSGBOX_DEFAULT)
        closemessage
        applymovement(OBJ_EVENT_ID_PLAYER, Walk1Right)
        waitmovement(0)
        clearflag(FLAG_SYSTEM_NOREWARDBATTLES)
        release
        end
    }
    waitse
    giveitem(ITEM_LEFTOVERS)
    buffernumberstring (STR_VAR_1, 30)
    setvar(VAR_0x8004, 30)
    special(GiveFrontierBattlePoints)
    playfanfare(MUS_OBTAIN_B_POINTS)
    msgbox("Earned {STR_VAR_1} BP!", MSGBOX_DEFAULT)
    waitfanfare
    msgbox(BuffetThankYouForDining, MSGBOX_DEFAULT)
    closemessage
    setflag(FLAG_BUFFET_COMPLETE)
    clearflag(FLAG_SYSTEM_NOREWARDBATTLES)
    applymovement(OBJ_EVENT_ID_PLAYER, Walk1Right)
    waitmovement(0)
    release
    end
}
text NotEnoughRoomLeftovers{
    "I'm sorry, but it looks like you don't\n"
    "have enough room in your bag.\p"
    "You need to clear some room first…"
}

script ExplainBuffetRules{
    hidemoneybox
    msgbox(ExplainBuffetRules_Text, MSGBOX_DEFAULT)
    goto(BuffetMultiChoice)
}
script BuffetComeBackLater{
    hidemoneybox
    msgbox(BuffetPleaseComeAgain, MSGBOX_DEFAULT)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Walk1Right)
    waitmovement(0)
    release
    end
}
script BuffetNotEnoughMoney{
    msgbox(BuffetNotEnoughMoney_Text, MSGBOX_DEFAULT)
    closemessage
    hidemoneybox
    applymovement(OBJ_EVENT_ID_PLAYER, Walk1Right)
    waitmovement(0)
    release
    end
}
script BuffetAlreadyCompleted{
    msgbox(BuffetAlreadyCompleted_Text, MSGBOX_DEFAULT)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Walk1Right)
    waitmovement(0)
    release
    end
}
text BuffetIntro{
    "Hello and welcome to the Battle\n"
    "Buffet!\p"
    "Our restaurant's menu is like\n"
    "no other, attracting experienced\l"
    "Pokémon trainers from all over\l"
    "the world!\p"
    "If you'd like to dine here, there are\n"
    "certain rules you must follow."
}
text BuffetWhatWouldYouHave{
    "How may I serve you?"
}
text EnjoyYourBuffet{
    "Thank you very much!\p"
    "When you're finished, come back\n"
    "here to obtain your fabulous prize!\p"
    "Enjoy your buffet!"
}
text ExplainBuffetRules_Text{
    "As I mentioned earlier, our customers\n"
    "are all experienced Pokémon trainers.\p"
    "There are currently 4 trainers dining\n"
    "here and if you manage to beat all\l"
    "of them, you'll get a fabulous prize!\p"
    "As this is a light-hearted and friendly\n"
    "competition, there are 4 special rules\l"
    "in play here:\p"
    "1. No trading of currency!\p"
    "That means that battlers here won't\n"
    "exchange money or BP when someone's\l"
    "team whites out. That also means you.\p"
    "2. No stealing from others!\p"
    "This applies to items the trainers'\n"
    "Pokémon are carrying.\l"
    "Any items consumed during the match,\l"
    "like for example berries, will not\l"
    "be returned.\p"
    "3. Win or lose!\p"
    "You must keep going until you either\n"
    "win or you team whites out.\l"
    "There is no forfeiting during\l"
    "the challenge!\p"
    "4. No stopping!\p"
    "Once the challenge has been started,\n"
    "you can no longer stop to heal your\l"
    "pokemon or switch party composition.\p"
    "You are however, allowed to have\n"
    "a meal to completely heal your team.\p"
    "Do note that this meal is available\n"
    "only once per attempt."
}
text BuffetPleaseComeAgain{
    "Thank you very much!\n"
    "Please come again!"
}
text BuffetThankYouForDining{
    "Thank you for partaking in the\n"
    "Battle Buffet!\l"
    "We hope you enjoyed the experience!"
}
text BuffetNotEnoughMoney_Text{
    "I'm sorry, but you don't have enough\n"
    "money to partake in the Battle Buffet…"
}
text BuffetCompleted{
    "Oh wow, it seems like we have\n"
    "a winner!\p"
    "For your peerless display of skill,\n"
    "you deserve a fabulous prize!"
}
text BuffetAlreadyCompleted_Text{
    "I'm sorry, but you have already\n"
    "competed in the Battle Buffet.\p"
    "It may take a while until we find\n"
    "new and stronger opponents for you."
}
text ThatWillBe1000{
    "Before you enter, I must warn you\n"
    "that the trainers here are extremely\l"
    "powerful.\p"
    "Would you still like to enter the\n"
    "dining hall? An entry costs ¥1000."
}

script Hotel_BuffetAttempt{
    msgbox(Hotel_BuffetAttempt_Text, MSGBOX_NPC)
    warpsilent(MAP_KAOLISLE_HOTEL_1F, 13, 7)
    waitstate
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    end
}
text Hotel_BuffetAttempt_Text{
    "I tried challenging the Battle\n"
    "Buffet and failed miserably.\l"
    "Those trainers are tough!"
}
script Hotel_FatMan{
    msgbox(Hotel_FatMan_Text, MSGBOX_NPC)
    end
}
text Hotel_FatMan_Text{
    "How do you like my Pokémon?\p"
    "Many say they don't see the appeal\n"
    "but for me he is perfect."
}
script Hotel_Child{
    msgbox(Hotel_Child_Text, MSGBOX_NPC)
    end
}
text Hotel_Child_Text{
    "Swim swim swim swim swim swim swim!"
}
script Hotel_Wife{
    msgbox(Hotel_Wife_Text, MSGBOX_NPC)
    end
}
text Hotel_Wife_Text{
    "Our little boy has been waiting to\n"
    "get to the beach all day."
}
script Hotel_Husband{
    msgbox(Hotel_Husband_Text, MSGBOX_NPC)
    end
}
text Hotel_Husband_Text{
    "I can't wait to relax at the beach.\p"
    "We'll be heading out as soon as\n"
    "our friends arrive."
}
script Hotel_Tuber{
    msgbox(Hotel_Tuber_Text, MSGBOX_NPC)
    end
}
text Hotel_Tuber_Text{
    "We're going to the beach!\p"
    "I've never swam before and feel a bit\n"
    "scared but I trust my Pokémon will\l"
    "keep me safe!"
}
script Hotel_BirdTrainer{
    if(flag(FLAG_HOTEL_FLYINGGEM)){
        msgbox(Hotel_BirdTrainer_PostBattle, MSGBOX_NPC)
        end
    }
    lockall
    faceplayer
    if(flag(FLAG_TEMP_3)){
        msgbox(Hotel_BirdTrainer_TextShort, MSGBOX_YESNO)
    }
    else{
        msgbox(Hotel_BirdTrainer_Text, MSGBOX_YESNO)
    }
    if(var(VAR_RESULT) == FALSE){
        setflag(FLAG_TEMP_3)
        msgbox(Hotel_BirdTrainer_Decline, MSGBOX_NPC)
        end
    }
    msgbox(Hotel_BirdTrainer_LetsDoThis)
    trainerbattle_no_intro(TRAINER_HOTEL_BIRDKEEPER, Hotel_BirdTrainer_Lose)
    msgbox(Hotel_BirdTrainer_Reward, MSGBOX_DEFAULT)
    checkitemspace(ITEM_FLYING_GEM)
    if(var(VAR_RESULT) == FALSE){
        msgbox(NotEnoughRoom, MSGBOX_NPC)
        end
    }
    giveitem(ITEM_FLYING_GEM, 1)
    waitse
    waitmessage
    closemessage
    setflag(FLAG_HOTEL_FLYINGGEM)
    release
    end
}
text Hotel_BirdTrainer_Text{
    "Hey, hi there!\n"
    "You look like a traveling trainer!\p"
    "Kaolisle is so nice and warm.\n"
    "It's totally the best, isn't it?\p"
    "I'm a traveling trainer as well.\p"
    "Being here with my cool bird Pokémon\n"
    "at a resort like this one is just what\l"
    "I needed to relax.\p"
    "Oh, I know!\p"
    "Since you stopped by to talk to me\n"
    "and all, why don't you battle me and\l"
    "my bird Pokémon before you go?"
}
text Hotel_BirdTrainer_TextShort{
    "Have a battle against me and my\n"
    "bird Pokémon!"
}
text Hotel_BirdTrainer_Decline{
    "Oh, what a shame!\p"
    "I'll be here for a while, so come back\n"
    "if you change your mind!"
}
text Hotel_BirdTrainer_LetsDoThis{
    "Thanks! Let's do this then!\n"
    "No need to hold back when facing me!"
}
text Hotel_BirdTrainer_Lose{
    "Oh come on now!\n"
    "Do you want me to sulk?"
}
text Hotel_BirdTrainer_PostBattle{
    "Thanks for the fun battle!\n"
    "It'll be a great memento of my journey!"
}
text Hotel_BirdTrainer_Reward{
    "Phew! You are STRONG!\n"
    "That was surprising!\p"
    "Maybe I was a bit hasty challenging\n"
    "you like that. You don't seem like\l"
    "a regular rookie!\p"
    "If you'd like, why don't you have your\n"
    "Pokémon hold on to this Flying Gem?\l"
    "It could make your moves even stronger!"
}
script Hotel_Drinker{
    msgbox(Hotel_Drinker_Text, MSGBOX_NPC)
    end
}
text Hotel_Drinker_Text{
    "You should consider picking up a\n"
    "Fresh Water or two from the\l"
    "vending machine.\l"
    "Your Pokémon will guzzle it right down!"

}
script Hotel_Attendant{
    msgbox(Hotel_Attendant_Text, MSGBOX_NPC)
    end
}
text Hotel_Attendant_Text{
    "Welcome to the Kaolisle Grand Resort.\p"
    "We're currently all booked up,\n"
    "but feel free to enjoy the ambiance."
}
script Hotel_PrivateParty{
    lockall
    playse(SE_PIN)
	applymovement(1, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(40)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    delay(20)
    msgbox(Hotel_PrivateParty_Text, MSGBOX_DEFAULT)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Walk1Left)
    waitmovement(0)
    release
    end
}
text Hotel_PrivateParty_Text{
    "I'm sorry, but this room\n"
    "is reserved for a private party."
}
script Hotel_Chef{
    msgbox(Hotel_Chef_Text, MSGBOX_NPC)
    end
}
text Hotel_Chef_Text{
    "Our staff thanks you for partaking\n"
    "in the Battle Buffet.\p"
    "Feel free to grab some food and\n"
    "drinks from the table.\p"
    "Do note that taking the food outside\n"
    "the buffet is prohibited."
}