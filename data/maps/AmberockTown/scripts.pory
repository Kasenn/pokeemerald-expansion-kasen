mapscripts AmberockTown_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: Amberock_FlagControl
    MAP_SCRIPT_ON_LOAD: Amberock_PaperVisibility
}

const X = VAR_TEMP_1
const Y = VAR_TEMP_2

script Amberock_FlagControl{
    setflag(FLAG_TEMP_3)
    setflag(FLAG_TEMP_2)
    if(var(VAR_ROUTE14_ROCKET_EVENT) >= 6){
        setflag(FLAG_TEMP_1)
    }
    if(flag(FLAG_BADGE05_GET) && !flag(FLAG_RECEIVED_HM_SURF)){
        setobjectxyperm(1, 33, 11)
        clearflag(FLAG_TEMP_2)
    }
}
script Amberock_PaperVisibility{
    if(var(VAR_ROUTE14_ROCKET_EVENT) >= 6){
        setmetatile(27, 10, 0x128, FALSE)
        setmetatile(27, 12, 0x189, FALSE)
        setmetatile(26, 12, 0x189, FALSE)
        setmetatile(25, 13, 0x170, FALSE)
        setmetatile(24, 13, 0x170, FALSE)
        setmetatile(23, 13, 0x170, FALSE)
        setmetatile(23, 14, 0x170, FALSE)
        setmetatile(24, 14, 0x170, FALSE)
        setmetatile(25, 14, 0x170, FALSE)
        setmetatile(24, 15, 0x170, FALSE)
        setmetatile(25, 15, 0x170, FALSE)
        setmetatile(26, 15, 0x170, FALSE)
        setmetatile(26, 16, 0x170, FALSE)
        setmetatile(27, 16, 0x170, FALSE)
        setmetatile(28, 16, 0x170, FALSE)
    }
}

script Amberock_SurfScientist{
    lock
    faceplayer
    msgbox(HereIsSurfHM)
    giveitem(ITEM_HM03)
    msgbox(GoGetBackPapers)
    closemessage
    if(var(VAR_FACING) == DIR_SOUTH){
        getfollowerxy(X, Y)
        if(var(X) != 34 && var(Y) != 10){
            safefollow
        }
        applymovement(1, ScientistLeavesDetour)
    }
    else{
        applymovement(1, ScientistLeavesStraight)
    }
    waitmovement(0)
    removeobject(1)
    setflag(FLAG_RECEIVED_HM_SURF)
    release
}
text HereIsSurfHM{
    "Ah, perfect timing!\p"
    "I was able to get my hands on the\n"
    "perfect Hidden Machine for this job.\p"
    "Here, you should take it."
}
text GoGetBackPapers{
    "As much as I would like to help you on\n"
    "this, I'm just a scientist after all.\p"
    "I mean, I don't even have my own\n"
    "Pokémon!\p"
    "I know this is a lot to ask from\n"
    "someone you barely even know,\l"
    "but please, recover our research\l"
    "material from Team Rocket!\p"
    "I had a talk with my colleagues and we\n"
    "have unanimously decided that if you\l"
    "do this, we'll reward you with\l"
    "something special.\p"
    "With that said, I need to get back to\n"
    "my research.\p"
    "When you're done, come meet me back at\n"
    "the research center on Route 14."
}

movement ScientistLeavesDetour{
    walk_right
    walk_up * 4
    walk_left
    walk_up * 2
    walk_left * 5
    walk_up * 2
}
movement ScientistLeavesStraight{
    walk_up * 5
    walk_left * 5
    walk_up * 2
}

script Amberock_Papers{
    lock
    safefollow
    msgbox(PapersOnGround)
    removeobject(3)
    setmetatile(27, 10, 0x128, FALSE)
    special(DrawWholeMapView)
    closemessage
    clearflag(FLAG_TEMP_2)
    clearflag(FLAG_TEMP_3)
    addobject(1)
    addobject(2)
    getfollowerxy(X, Y)
    safefollow
    if(!defeated(TRAINER_R15_TR18_BERRYROCKET)){
        call(AmberockAlternativeCutscene)
    }
    delay(60)
    switch(var(VAR_FACING)){
        case DIR_SOUTH:
            if(var(X) == 27 && var(Y) == 8){
                clearflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
            }
            applymovement(1, MoveScientist2)
        case DIR_WEST:
            if(var(X) == 28 && var(Y) == 9){
                clearflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
            }
            setobjectxy(1, 28, 4)
            applymovement(1, Walk5Down)
        case DIR_EAST:
            if(var(X) == 26 && var(Y) == 9){
                clearflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
            }
            setobjectxy(1, 28, 4)
            applymovement(1, MoveScientist1)
    }
    waitmovement(0)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    delay(24)
    if(flag(FLAG_TEMP_A)){
        msgbox(OurPapersAlternative)
    }
    else{
        msgbox(OurPapers)
    }
    closemessage
    switch(var(VAR_FACING)){
        case DIR_SOUTH:
            applymovement(1, ScientistLeave2)
        case DIR_WEST:
            applymovement(1, Walk5Up)
        case DIR_EAST:
            applymovement(1, ScientistLeave1)
    }
    waitmovement(0)
    delay(60)
    switch(var(VAR_FACING)){
        case DIR_SOUTH:
            applymovement(2, MoveScientist2)
        case DIR_WEST:
            setobjectxy(2, 28, 4)
            applymovement(2, Walk5Down)
        case DIR_EAST:
            setobjectxy(2, 28, 4)
            applymovement(2, MoveScientist1)
    }
    waitmovement(0)
    delay(15)
    checkplayergender
    if(var(VAR_RESULT) == MALE){
        bufferstring(STR_VAR_1, "mister")
    }
    else{
        bufferstring(STR_VAR_1, "miss")
    }
    if(flag(FLAG_ACEROLA_TALKED_TO)){
        msgbox(AcerolaVisitGym)
    }
    else{
        msgbox(AcerolaVisitGymAlternative)
    }
    closemessage
    switch(var(VAR_FACING)){
        case DIR_SOUTH:
            applymovement(2, ScientistLeave2)
        case DIR_WEST:
            applymovement(2, Walk5Up)
        case DIR_EAST:
            applymovement(2, ScientistLeave1)
    }
    waitmovement(0)
    setvar(VAR_ROUTE14_ROCKET_EVENT, 6)
    removeobject(1)
    removeobject(2)
    delay(15)
    release
}
text PapersOnGround{
    "There seems to be some papers on\n"
    "the ground here.\p"
    "{PLAYER} picked up the papers."
}
text OurPapers{
    "Can I see those papers?\p"
    "{PLAYER} handed the papers to the\n"
    "scientist.\p"
    "… … … …\p"
    "Yes, these are definitely some of\n"
    "the papers that were stolen.\p"
    "The culprit must have fled to Amberock\n"
    "Town and cut the bridge behind him,\l"
    "giving him ample time to hide.\p"
    "That scoundrel!\p"
    "It would take too long to wait for\n"
    "the bridge to be repaired.\p"
    "You should prepare your team for\n"
    "what lies ahead.\p"
    "In the meantime, I'll try to get my\n"
    "hands on a Hidden Machine that can\l"
    "teach your Pokémon how to carry\l"
    "people across water.\p"
    "I'll meet you back here once we're\n"
    "both ready."
}
text OurPapersAlternative{
    "Can I see those papers?\p"
    "{PLAYER} handed the papers to the\n"
    "scientist.\p"
    "… … … …\p"
    "Yes, these are definitely some of\n"
    "the papers that were stolen.\p"
    "You say you saw the culprit flee to\n"
    "Amberock Town and cut the bridge\l"
    "behind him?\p"
    "That scoundrel!\p"
    "No matter what we do now, this\n"
    "has given him ample time to hide.\p"
    "It would take too long to wait for\n"
    "the bridge to be repaired.\p"
    "You should prepare your team for\n"
    "what lies ahead.\p"
    "In the meantime, I'll try to get my\n"
    "hands on a Hidden Machine that can\l"
    "teach your Pokémon how to carry\l"
    "people across water.\p"
    "I'll meet you back here once we're\n"
    "both ready."
}
text AcerolaVisitGym{
    "Acerola: I couldn't help overhearing\n"
    "parts of your conversation.\p"
    "If you're looking to strengthen your\n"
    "Pokémon, you should totally check the\l"
    "Pokémon Gym over in Drisledge Town!\p"
    "I hear it's one of the best gyms in\n"
    "all of Sinko!\p"
    "Anyway, I need to head back home now.\n"
    "See you again, {STR_VAR_1}!"
}
text AcerolaVisitGymAlternative{
    "I couldn't help overhearing\n"
    "parts of your conversation.\p"
    "If you're looking to strengthen your\n"
    "Pokémon, you should totally check the\l"
    "Pokémon Gym over in Drisledge Town!\p"
    "I hear it's one of the best gyms in\n"
    "all of Sinko!\p"
    "Anyway, I need to head back home now.\n"
    "See you again, {STR_VAR_1}!"
}
movement MoveScientist1{
    walk_down * 2
    walk_left * 2
    walk_down * 3
    step_end
}
movement MoveScientist2{
    walk_down * 2
    walk_left
    walk_down * 3
    step_end
}
movement ScientistLeave2{
    walk_up * 2
    walk_right
    walk_up * 3
    step_end
}
movement ScientistLeave1{
    walk_up * 2
    walk_right * 2
    walk_up * 3
    step_end
}

script AmberockAlternativeCutscene{
    delay(48)
    switch(var(VAR_FACING)){
        case DIR_WEST:
        case DIR_EAST:
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
    }
    waitmovement(0)
    special(SpawnCameraObject)
    if(var(VAR_FACING) == DIR_SOUTH){
        applymovement(OBJ_EVENT_ID_CAMERA, Walk5Down)
    }
    else{
        applymovement(OBJ_EVENT_ID_CAMERA, Walk4Down)
    }
    waitmovement(0)
    delay(60)
    msgbox(RocketYoureTooLate)
    closemessage
    setflag(FLAG_TEMP_A)
    applymovement(4, Walk4Down)
    waitmovement(0)
    if(var(VAR_FACING) == DIR_SOUTH){
        applymovement(OBJ_EVENT_ID_CAMERA, Walk5Up)
    }
    else{
        applymovement(OBJ_EVENT_ID_CAMERA, Walk4Up)
    }
    waitmovement(0)
    special(RemoveCameraObject)
    return
}

text RocketYoureTooLate{
    "Rocket Grunt: I see our little\n"
    "diversion wasn't enough to fool you.\p"
    "But, it matters not.\n"
    "You're late anyway!\p"
    "Good luck ever catching me!"
}