mapscripts Pearlwood_MaysHouse2F_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: Pearlwood_MaysHouse2F_SetRival
}

script Pearlwood_MaysHouse2F_SetRival{
    checkplayergender
    if(var(VAR_RESULT) == FEMALE){
        setmaplayoutindex(LAYOUT_RIVAL_HOUSE2F_BRENDAN)
    }
    call(Common_EventScript_SetupRivalGfxId)
}

script Pearlwood_MaysHouse2F_Rival{
    lock
    if(var(VAR_PEARLWOOD_TOWN_STATE) == 2){
        waitse
        playse(SE_PIN)
        safefollow
        applymovement(3, Common_Movement_ExclamationMark)
        waitmovement(0)
        faceplayer
        delay(48)
        msgbox(Pearlwood_MaysHouse2F_Intro)
        closemessage
        delay(20)
        playse(SE_PIN)
        applymovement(3, Common_Movement_ExclamationMark)
        waitmovement(0)
        delay(48)
        setvar(VAR_PEARLWOOD_TOWN_STATE, 3)
        checkplayergender
        if(var(VAR_RESULT) == MALE){
            msgbox(Pearlwood_MaysHouse2F_Intro2_MalePC)
        }
        else{
            msgbox(Pearlwood_MaysHouse2F_Intro2_FemalePC)
        }
    }
    else{
        msgbox(Pearlwood_MaysHouse2F_Gossip)
    }
    turnobject(3, DIR_NORTH)
    release
}

text Pearlwood_MaysHouse2F_Intro{
    "{RIVAL}: Oh, hi {PLAYER}!\p"
    "Sorry, I'm a bit busy answering to\n"
    "some e-mail.\p"
    "I heard that my dad\n"
    "was looking for you."
}

text Pearlwood_MaysHouse2F_Intro2_MalePC{
    "I get it now!\n"
    "I bet it's time for you to finally\l"
    "get your own Pokémon!\l"
    "I hope you pick a cute one!\p"
    "Dad's probably waiting for you\n"
    "in his lab to the northwest."
}

text Pearlwood_MaysHouse2F_Intro2_FemalePC{
    "I get it now!\n"
    "I bet it's time for you to finally\l"
    "get your own Pokémon!\l"
    "I hope you pick a cool one!\p"
    "Dad's probably waiting for you\n"
    "in his lab to the northwest."
}

text Pearlwood_MaysHouse2F_Gossip{
    "{RIVAL}: Sorry, but I REALLY need to\n"
    "finish writing this e-mail."
}
