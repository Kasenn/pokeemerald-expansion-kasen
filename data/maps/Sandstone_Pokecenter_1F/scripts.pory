mapscripts Sandstone_Pokecenter_1F_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: SandstonePokeCenter1F_OnTransition
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_DEBUG, 8: AutomaticUpdate
        VAR_DEBUG, 9: AutomaticUpdate2
    ]
	// MAP_SCRIPT_ON_RESUME: CableClub_OnResume
}

script SandstonePokeCenter1F_OnTransition{
	setrespawn(HEAL_LOCATION_SANDSTONE_CITY)
	call(Common_EventScript_UpdateBrineyLocation)
	end
}

script SandstonePokeCenter1F_EventScript_Nurse{
	setvar(VAR_0x800B, 1)
	call(Common_EventScript_PkmnCenterNurse)
	waitmessage
	waitbuttonpress
	release
	end
}

script SandstonePokeCenter1F_Youngster{
    msgbox(SandstonePokeCenter1F_Youngster_Text, MSGBOX_NPC)
    end
}
script SandstonePokeCenter1F_Pokefan{
    msgbox(SandstonePokeCenter1F_Pokefan_Text, MSGBOX_NPC)
    end
}

script SandstonePokeCenter1F_StarterGift{
    lock
    faceplayer
    msgbox(TryOutFeature, MSGBOX_YESNO)
    if(var(VAR_RESULT) == FALSE){
        closemessage
        release
        end
    }
    if(!flag(FLAG_OBTAINED_MEGA_RING)){
        msgbox(MustClearTowerFirst, MSGBOX_NPC)
        end
    }
    message(WhichMonWouldYouTest)
    waitmessage
    multichoice(23, 0, MULTI_TESTMEGASTARTER, 0)
    switch(var(VAR_RESULT)){
        case 0:
            givemon(SPECIES_DECIDUEYE, 40, ITEM_DECIDUEITE)
            bufferspeciesname(STR_VAR_1, SPECIES_DECIDUEYE)
            setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_DECIDUEYE)
            goto(ReceiveMegaStarter)
        case 1:
            givemon(SPECIES_BLAZIKEN, 40, ITEM_BLAZIKENITE)
            bufferspeciesname(STR_VAR_1, SPECIES_BLAZIKEN)
            setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_BLAZIKEN)
            goto(ReceiveMegaStarter)
        case 2:
            givemon(SPECIES_EMPOLEON, 40, ITEM_EMPOLEONITE)
            bufferspeciesname(STR_VAR_1, SPECIES_EMPOLEON)
            setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_EMPOLEON)
            goto(ReceiveMegaStarter)
        case 3:
            givemon(SPECIES_CHESNAUGHT, 40, ITEM_CHESNAUGHTITE)
            bufferspeciesname(STR_VAR_1, SPECIES_CHESNAUGHT)
            setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_CHESNAUGHT)
            goto(ReceiveMegaStarter)
        case 4:
            givemon(SPECIES_INCINEROAR, 40, ITEM_INCINEROARITE)
            bufferspeciesname(STR_VAR_1, SPECIES_INCINEROAR)
            setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_INCINEROAR)
            goto(ReceiveMegaStarter)
        case 5:
            givemon(SPECIES_SWAMPERT, 40, ITEM_SWAMPERTITE)
            bufferspeciesname(STR_VAR_1, SPECIES_SWAMPERT)
            setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_SWAMPERT)
            goto(ReceiveMegaStarter)
        case MULTI_B_PRESSED:
            closemessage
            release
            end
    }
}

text SandstonePokeCenter1F_Youngster_Text{
    "I recently took one of my Pokémon back\n"
    "from the Pokémon Day Care and it had\l"
    "grown twice what I had expected!"
}
text SandstonePokeCenter1F_Pokefan_Text{
    "Have you heard?\p"
    "Many stores have lowered the cost of\n"
    "Max Repels, making them more\l"
    "cost-effective than Super Repels."
}

script ReceiveMegaStarter{
    call(ReceivedMegaMonFanfare)
    if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
        goto(ReceiveMegaParty)
    }
    if(var(VAR_RESULT) == MON_GIVEN_TO_PC){
        goto(ReceiveMegaPC)
    }
    goto(Common_EventScript_NoMoreRoomForPokemon)
    end
}

script ReceiveMegaParty{
	msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO){
        closemessage
        release
        end
    }
	call(Common_EventScript_GetGiftMonPartySlot)
	call(Common_EventScript_NameReceivedPartyMon)
	closemessage
    release
    end
	end
}
script ReceiveMegaPC{
	msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
	if(var(VAR_RESULT) == FALSE){
		call(Common_EventScript_TransferredToPC)
		closemessage
        release
        end
	}
	call(Common_EventScript_NameReceivedBoxMon)
	call(Common_EventScript_TransferredToPC)
	closemessage
    release
	end
}

script ReceivedMegaMonFanfare{
    playfanfare(MUS_OBTAIN_ITEM)
    message(ReceivedMegaMon)
    waitmessage
    waitfanfare
    return
}

text ReceivedMegaMon{
    "Received {STR_VAR_1}!"
}

text TryOutFeature{
    "I'm a temporary NPC to help test out\n"
    "a feature.\p"
    "Would you like to test the feature?"
}
text MustClearTowerFirst{
    "I'm sorry, but you are not ready\n"
    "for this feature yet.\l"
    "You must progress further in\l"
    "the story first."
}
text WhichMonWouldYouTest{
    "Which Pokémon would you like to test?"
}