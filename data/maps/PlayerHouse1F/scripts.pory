mapscripts PlayerHouse1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: PH1FClearFlag
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_PEARLWOOD_TOWN_STATE, 1: PlayerHouse1F_MomIntro
    ]
}

script PlayerHouse1F_MomIntro{
    lock
    turnobject(1, DIR_NORTH)
    playse(SE_PIN)
    applymovement(1, Common_Movement_ExclamationMark)
    waitmovement(0)
    delay(48)
    // msgbox(PlayerHouse1F_LookPlayer)
    // closemessage
    // delay(30)
    // call(IntroNameBrother)
    msgbox(PlayerHouse1F_LookPlayer)
    closemessage
    turnobject(1, DIR_WEST)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerHouse1F_PlayerSitTable)
    waitmovement(0)
    delay(20)
    msgbox(PlayerHouse1F_Disc1)
    closemessage
    fadescreen(FADE_TO_BLACK)
    delay(80)
    fadescreen(FADE_FROM_BLACK)
    msgbox(PlayerHouse1F_Disc2)
    closemessage
    playse(SE_PIN)
    applymovement(1, Common_Movement_ExclamationMark)
    waitmovement(0)
    delay(48)
    turnobject(1, DIR_SOUTH)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    msgbox(PlayerHouse1F_Disc3)
    closemessage
    setflag(FLAG_BROTHER_PEARLFIGHT)
    setvar(VAR_PEARLWOOD_TOWN_STATE, 2)
    call(Player1F_Shoes)
    turnobject(1, DIR_WEST)
    release
}

// script IntroNameBrother{
//     fadescreen(FADE_TO_BLACK)
//     special(NameBrother)
//     waitstate
//     msgbox(SoItsBrother, MSGBOX_YESNO)
//     if(var(VAR_RESULT) == FALSE){
//         goto(BrotherLoop)
//     }
//     closemessage
//     return
// }
// text SoItsBrother{
//     "So it's {BROTHER}?"
// }
// script BrotherLoop{
//     goto(IntroNameBrother)
// }

script Player1F_Shoes{
    msgbox("Oh, and one more thing.")
    playfanfare(MUS_OBTAIN_ITEM)
    message("{COLOR RED}{SHADOW LIGHT_RED}Obtained the running shoes!")
    waitfanfare
    waitmessage
    setflag(FLAG_SYS_B_DASH)
    setflag(FLAG_RECEIVED_RUNNING_SHOES)
    msgbox("Mom: Now you can zip around everywhere\nmuch faster!")
    closemessage
    delay(30)
}

text PlayerHouse1F_LookPlayer{
    "Mom: Look, {PLAYER}!\n"
    "Your brother is back from his trip!\p"
    "Come, come sit by the table!"
}
text PlayerHouse1F_LookPlayer2{
    "Mom: Come, come sit by the table!"
}

movement PlayerHouse1F_PlayerSitTable{
    walk_down * 4
    walk_left * 3
}

text PlayerHouse1F_Disc1{
    "Mom: So, {BROTHER},\n"
    "how was your trip to Kanto?\p"
    "Go on, I want to hear everything!\p"
    "{BROTHER}: …"
}

text PlayerHouse1F_Disc2{
    "{BROTHER}: In the end, that Lance's\n"
    "Dragonite was simply too strong…\p"
    "Mom: Well, at least you tried\n"
    "your best.\p"
    "It sounds like you still had fun\n"
    "in the end!\p"
    "{BROTHER}: Anyway mom, aren't you\n"
    "forgetting something?"
}

text PlayerHouse1F_Disc3{
    "Mom: Oh, that's right!\p"
    "This is supposed to be your special day,\n"
    "{PLAYER}!\p"
    "That's right! Today is the day you get\n"
    "your very own Pokémon!\p"
    "Every young trainer in this town\n"
    "gets their first Pokémon from\l"
    "professor Birch.\p"
    "You should go see him in his lab\n"
    "and see what he has to offer.\p"
    "The professor's lab is just\n"
    "northwest from here."
}

text PlayerHouse1F_BrotherGossip{
    "{BROTHER}: Go get 'em, champ!"
}

script PlayerHouse1F_Brother{
    msgbox(PlayerHouse1F_BrotherGossip, MSGBOX_NPC)
    turnobject(2, DIR_EAST)
}

script PlayerHouse1F_Mom{
    lockall
    faceplayer
    if((var(VAR_PEARLWOOD_TOWN_STATE) >= 15)
    && (!flag(FLAG_RECEIVED_CAPSULE))){
        msgbox(DadLeftAGift, MSGBOX_NPC)
        setflag(FLAG_DAD_INTRO)
        setflag(FLAG_RECEIVED_CAPSULE)
        clearflag(FLAG_DAD_GIFT)
        end
    }
    elif(var(VAR_PEARLWOOD_TOWN_STATE) == 2){
        msgbox(MomSeeYou, MSGBOX_NPC)
        turnobject(1, DIR_WEST)
    }
    elif(var(VAR_PEARLWOOD_TOWN_STATE) == 6){
        msgbox(PlayerHouse1F_MomGoodbye_Text, MSGBOX_NPC)
        setvar(VAR_PEARLWOOD_TOWN_STATE, 7)
        turnobject(1, DIR_WEST)
    }
    else{
        if(var(VAR_PEARLWOOD_TOWN_STATE) >= 10){
            if(flag(FLAG_DAD_INTRO)){
                goto(PlayersHouse_1F_EventScript_MomHealsParty)
            }
            msgbox(MomLookWhosBack, MSGBOX_NPC)
            setflag(FLAG_DAD_INTRO)
            end
        }
        goto(PlayersHouse_1F_EventScript_MomHealsParty)
    }
}

text MomSeeYou{
    "Mom: See you, honey!"
}
text PlayerHouse1F_MomGoodbye_Text{
    "Mom: You're leaving on an adventure?\n"
    "Well, can't say that I'm surprised.\p"
    "Your father was like that as well\n"
    "when we were young.\p"
    "Speaking of which, your father has been\n"
    "working hard to get accepted into\l"
    "the Pokémon League.\p"
    "Maybe one day you'll challenge him to\n"
    "a Pokémon battle on League grounds.\p"
    "If your Pokémon ever get hurt,\n"
    "come see me and I'll help patch them up."
}

text PlayerHouse1F_Apology{
    "{BROTHER}: Sorry about that.\n"
    "I knew that with my experience it was\l"
    "going to be an unfair fight. I hope you\l"
    "don't take that loss too seriously…\p"
    "Anyway, have you thought about what\n"
    "you're going to do next?\p"
    "If you're up for it, we should totally\n"
    "see which of us is the better trainer\l"
    "by taking on the Pokémon Gym Challenge!\p"
    "Our neighbor {RIVAL} just set out so we\n"
    "better get moving or we'll fall behind!\p"
    "Don't know about you but I'm not content\n"
    "with a second place.\p"
    "Catch ya later!"
}

script PH1FClearFlag{
    checkplayergender
    if(var(VAR_RESULT) == FEMALE){
        setmaplayoutindex(LAYOUT_PLAYER_HOUSE1F_MAY)
    }
    if(var(VAR_PEARLWOOD_TOWN_STATE) >= 10){
        clearflag(FLAG_HIDE_PLAYERS_HOUSE_DAD)
    }
    if(var(VAR_PEARLWOOD_TOWN_STATE) >= 15){
        setflag(FLAG_HIDE_PLAYERS_HOUSE_DAD)
    }
    if(var(VAR_PEARLWOOD_TOWN_STATE) >= 20){
        setflag(FLAG_DAD_INTRO)
    }
    end
}

script WooperDoll{
    msgbox("It's a Wooper doll.", MSGBOX_SIGN)
}
script AzurillDoll{
    msgbox("It's an Azurill doll.", MSGBOX_SIGN)
}

script PlayerHouse1F_Dad{
    lock
    faceplayer
    if(flag(FLAG_RECEIVED_CAPSULE)){
        msgbox(WhereBrother, MSGBOX_NPC)
        end
    }
    if(flag(FLAG_TEMP_5)){
        msgbox(GiftFromDadShort)
    }
    else{
        msgbox(GiftFromDad)
    }
    closemessage
    checkitemspace(ITEM_ABILITY_CAPSULE)
    if(var(VAR_RESULT) == FALSE){
        msgbox(NotEnoughRoom, MSGBOX_NPC)
        setflag(FLAG_TEMP_5)
        end
    }
    giveitem(ITEM_ABILITY_CAPSULE)
    waitse
    msgbox(ExplainCapsule, MSGBOX_NPC)
    setflag(FLAG_RECEIVED_CAPSULE)
    if(var(VAR_PEARLWOOD_TOWN_STATE) == 12){
        setvar(VAR_PEARLWOOD_TOWN_STATE, 13)
    }
    end
}

text GiftFromDad{
    "Dad: Hm?\p"
    "Well, if it isn't {PLAYER}!\p"
    "I had hoped I could see you and\n"
    "{BROTHER} before you flew out the door,\l"
    "but the exams at the Pokémon League\l"
    "have kept me incredibly busy.\p"
    "Still, I can't believe how much you've\n"
    "grown. You must have at least, what,\l"
    "three or four Gym Badges by now?\p"
    "Oh!\p"
    "I almost forgot. I picked up something\n"
    "during my travels that both you and\l"
    "{BROTHER} would certainly find useful.\p"
    "It's a shame {BROTHER} isn't here,\n"
    "but you can still have yours."
}

text GiftFromDadShort{
    "Dad: It's a shame {BROTHER} isn't here,\n"
    "but you can still have yours.\p"
}
text ExplainCapsule{
    "Dad: That's an Ability Capsule.\p"
    "Many Pokémon are born with one of two\n"
    "different abilities.\p"
    "Using it on a Pokémon will cause it\n"
    "to adapt and change its innate ability.\p"
    "Do note that the Ability Capsule can\n"
    "be used only once.\l"
    "Make sure you decide carefully which\l"
    "Pokémon to use it on."
}
text WhereBrother{
    "Dad: Have you seen {BROTHER} lately?\p"
    "Seeing how well he managed during\n"
    "the Kanto challenge, I hardly doubt\l"
    "his abilities.\p"
    "However, as a parent, I do still feel\n"
    "a bit anxious knowing you two are\l"
    "out there, traveling the world."
}
text MomLookWhosBack{
    "Mom: Oh, great timing, honey!\n"
    "Look who's back home!"
}
text DadLeftAGift{
    "Mom: Oh, hi honey!\p"
    "It's a shame you two just missed each\n"
    "other, but dad came to visit while\l"
    "you were away!\p"
    "I believe he left a gift for you in\n"
    "your room. You should go have a look!"
}