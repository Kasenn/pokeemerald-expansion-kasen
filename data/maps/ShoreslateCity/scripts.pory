mapscripts ShoreslateCity_MapScripts{
    MAP_SCRIPT_ON_LOAD: VisitedShoreslate
    MAP_SCRIPT_ON_TRANSITION: VisitedShoreslate
}
script VisitedShoreslate{
    if(!flag(FLAG_DAILY_RESET_CASTELIACONE)){
        setvar(VAR_CASTELIACONES_BOUGHT, 0)
        setflag(FLAG_DAILY_RESET_CASTELIACONE)
    }
    setflag(FLAG_VISITED_SHORESLATE)
    setflag(FLAG_ROUTE28_DESPAWN_BROTHER)
    end
}

script Shoreslate_NinjaBoy{
    if(!flag(FLAG_HELPED_DEVON_CORP)){
    msgbox(Shoreslate_NinjaBoy_Text, MSGBOX_NPC)
    }
    else{
        msgbox(Shoreslate_NinjaBoy_Text2, MSGBOX_NPC)
    }
    end
}
text Shoreslate_NinjaBoy_Text{
    "I wanted to visit Devon Corp's offices\n"
    "but there was this strange man who\l"
    "told me to leave…"
}
text Shoreslate_NinjaBoy_Text2{
    "Oh, the weird man outside was\n"
    "a Team Rocket grunt?\l"
    "Glad to see they were able to\l"
    "drive him away."
}
script Shoreslate_OldWoman{
    msgbox(Shoreslate_OldWoman_Text, MSGBOX_NPC)
    end
}
text Shoreslate_OldWoman_Text{
    "I used to be the owner of the Pretty\n"
    "Petal flower shop on route 8.\l"
    "Nowadays I'm retired so my daughter has\l"
    "taken over the business in my stead."
}
script Shoreslate_Boy1{
    msgbox(Shoreslate_Boy1_Text, MSGBOX_NPC)
    end
}
text Shoreslate_Boy1_Text{
    "Oh, I don't believe I've seen\n"
    "your face here before.\p"
    "Are you here to visit or are you just\n"
    "passing through on your way to\l"
    "western Sinko?"
}
script Shoreslate_ItemBall{
    finditem(ITEM_FULL_HEAL)
    end
}
script Shoreslate_Man2{
    msgbox(Shoreslate_Man2_Text, MSGBOX_NPC)
    end
}
text Shoreslate_Man2_Text{
    "Do you have the need for speed?\p"
    "Then you need to visit Rydel's\n"
    "bike shop!"
}
script Shoreslate_Sign1{
    msgbox("SHORESLATE CITY\p"
    "“The port where people and Pokémon\n"
	"cross paths.”", MSGBOX_SIGN)
    end
}
script Shoreslate_Sign2{
    msgbox("Devon Corporation Headquarters", MSGBOX_SIGN)
    end
}
script Shoreslate_Sign3{
    msgbox("Shoreslate Harbor", MSGBOX_SIGN)
    end
}
script Shoreslate_Sign4{
    msgbox("Shoreslate Bike Shop", MSGBOX_SIGN)
    end
}
script Shoreslate_Sailor{
    msgbox(Shoreslate_Sailor_Text, MSGBOX_NPC)
    end
}
text Shoreslate_Sailor_Text{
    "Taking the ferry is a perfectly fine\n"
    "option, but if you want to feel alive,\l"
    "there's no better way of crossing the\l"
    "ocean than on the back of a Pokémon!"
}
script Shoreslate_ExpertM{
    lockall
    msgbox(Shoreslate_ExpertM_Text, MSGBOX_DEFAULT)
    waitmessage
    closemessage
    release
    end
}
text Shoreslate_ExpertM_Text{
    "Ah, the sea…\n"
    "Makes you glad to be alive.\l"
    "Just feel that salty spray."
}
script Shoreslate_Maniac{
    msgbox(Shoreslate_Maniac_Text, MSGBOX_NPC)
    end
}
text Shoreslate_Maniac_Text{
    "I really want to visit the market but\n"
    "there's just so many people there and\l"
    "I don't do great with crowds…"
}
script Shoreslate_OldCoupleM{
    msgbox(Shoreslate_OldCoupleM_Text, MSGBOX_NPC)
    end
}
text Shoreslate_OldCoupleM_Text{
    "Shoreslate is one of the oldest cities\n"
    "in all of Sinko.\p"
    "My wife and I were both born here and\n"
    "even back in our youth, Shoreslate\l"
    "was already a bustling city."
}
script Shoreslate_OldCoupleF{
    msgbox(Shoreslate_OldCoupleF_Text, MSGBOX_NPC)
    end
}
text Shoreslate_OldCoupleF_Text{
    "A young trainer, eh?\p"
    "Don't let my looks deceive you.\n"
    "In my youth, I came this close to\l"
    "becoming the Pokémon League Champion\l"
    "of Sinko."
}
script Shoreslate_Woman3{
    msgbox(Shoreslate_Woman3_Text, MSGBOX_NPC)
    end
}
text Shoreslate_Woman3_Text{
    "Have you been to a BP Shop yet?\n"
    "I heard this one's selection of moves\l"
    "and items is completely different\l"
    "compared to the one in Aldeleaf."
}
script Shoreslate_Fisher{
    msgbox(Shoreslate_Fisher_Text, MSGBOX_NPC)
    end
}
text Shoreslate_Fisher_Text{
    "I was once fishing off the coast of\n"
    "Kaolisle City when I almost caught\l"
    "a Wailord!\p"
    "Don't believe me?\n"
    "Ask my friend!"
}
script Shoreslate_Man1{
    msgbox(Shoreslate_Man1_Text, MSGBOX_NPC)
    end
}
text Shoreslate_Man1_Text{
    "Everything this guy says is true!\p"
    "… … … …\p"
    "Okay, it was actually just a regular\n"
    "Goldeen…"
}

script Shoreslate_CasteliaGirl{
    faceplayer
    lockall
    msgbox(Shoreslate_CasteliaGirl_Text, MSGBOX_DEFAULT)
    waitmessage
    closemessage
    delay(15)
    checkitem(ITEM_CASTELIACONE, 1)
    if(var(VAR_RESULT) == TRUE){
        if(var(VAR_CASTELIACONES_BOUGHT) == 1){
            msgbox(ShoreslateCheckCastelia, MSGBOX_YESNO)
        }
        else{
            msgbox(ShoreslateCheckCastelias, MSGBOX_YESNO)
        }
        if(var(VAR_RESULT) == TRUE){
            checkitem(ITEM_CASTELIACONE, 4)
            if(var(VAR_RESULT) == FALSE){
                msgbox(Shoreslate_CasteliaSecondThought, MSGBOX_NPC)
            }
            if(var(VAR_RESULT) == TRUE){
                removeitem(ITEM_CASTELIACONE, 4)
                playfanfare(MUS_OBTAIN_ITEM)
                message(HandedOverCastelia)
                waitfanfare
                checkplayergender
                if(var(VAR_RESULT) == MALE){
                    msgbox(ThankYouMrCastelia, MSGBOX_DEFAULT)
                }
                else{
                    msgbox(ThankYouMsCastelia, MSGBOX_DEFAULT)
                }
                giveitem(ITEM_AIR_BALLOON)
                waitfanfare
                msgbox(GetHomeFast, MSGBOX_DEFAULT)
                closemessage
                fadescreen(FADE_TO_BLACK)
                delay(15)
                removeobject(24)
                setflag(FLAG_GIVEN_CASTELIATOGIRL)
                fadescreen(FADE_FROM_BLACK)
                delay(15)
            }
        }
    }
    closemessage
    waitmessage
    release
    end
}
text Shoreslate_CasteliaGirl_Text{
    "Sniff… My daddy gave me money to buy\n"
    "ice cream for the whole family but\l"
    "when I got here, I could no longer\l"
    "find the bill in my pocket…\l"
    "What do I do…"
}
text ShoreslateCheckCastelia{
    "You recall buying a Casteliacone.\n"
    "Would you like to offer it to the\l"
    "little girl?"
}
text ShoreslateCheckCastelias{
    "You recall buying some Casteliacones.\n"
    "Would you like to offer them to the\l"
    "little girl?"
}
text Shoreslate_CasteliaSecondThought{
    "On second thought, maybe you should\n"
    "get some more Casteliacones first.\l"
    "She did mention “the whole family.”"
}
text HandedOverCastelia{
    "{PLAYER} handed 4 Casteliacones\n"
    "to the little girl."
}
text ThankYouMrCastelia{
    "Wow! Thanks mister!\n"
    "You are the best!\p"
    "Umm, my hands are getting a bit full\n"
    "so here, you can have this."
}
text ThankYouMsCastelia{
    "Wow! Thanks big sis!\n"
    "You are the best!\p"
    "Umm, my hands are getting a bit full\n"
    "so here, you can have this."
}
text GetHomeFast{
    "I need to get back before these melt.\n"
    "Thanks again!"
}

script Shoreslate_CasteliaVendor{
    if(var(VAR_CASTELIACONES_BOUGHT) >= 8){
        msgbox(Shoreslate_CasteliaVendor_OutOfCones, MSGBOX_NPC)
        end
    }
    faceplayer
    lock
    if(var(VAR_TEMP_5) == 0){
        showmoneybox(0, 0)
        setvar(VAR_TEMP_5, 1)
        msgbox(Shoreslate_CasteliaVendor_InterestInCones, MSGBOX_YESNO)
        if(var(VAR_RESULT) == TRUE){
            goto(HowManyCastelias)
            end
        }
        else{
            msgbox(Shoreslate_CasteliaVendor_ThatsShame, MSGBOX_NPC)
            hidemoneybox
            end
        }
    }
    showmoneybox(0, 0)
    msgbox(Shoreslate_CasteliaVendor_InterestInConesShort, MSGBOX_YESNO)
        if(var(VAR_RESULT) == TRUE){
            goto(HowManyCastelias)
            end
        }
        else{
            msgbox(Shoreslate_CasteliaVendor_ThatsShame, MSGBOX_NPC)
            hidemoneybox
            end
        }
}
script HowManyCastelias{
    message("How many Casteliacones\nwould you like to buy?")
    multichoice(23, 0, 9, 0)
    switch(var(VAR_RESULT)){
        case 0:
            goto(Buy1Castelia)
        case 1:
            goto(Buy4Castelia)
        case 2:
            goto(CancelBuyingCastelia)
        case MULTI_B_PRESSED:
            goto(CancelBuyingCastelia)
    }
}
script Buy1Castelia{
    hidemoneybox
    checkitemspace(ITEM_CASTELIACONE, 1)
    if(var(VAR_RESULT) == FALSE){
        msgbox(NotRoomForCastelias, MSGBOX_NPC)
        end
    }
    checkmoney(350)
    if(var(VAR_RESULT) == FALSE){
        msgbox(NotMoneyForCastelias, MSGBOX_NPC)
        end
    }
    playfanfare(MUS_OBTAIN_ITEM)
    message(Bought1Castelia)
	waitfanfare
	msgbox(Put1CasteliaInPocket, MSGBOX_DEFAULT)
    removemoney(350)
    additem(ITEM_CASTELIACONE, 1)
    addvar(VAR_CASTELIACONES_BOUGHT, 1)
    waitmessage
    closemessage
    msgbox("Please come again!", MSGBOX_NPC)
    release
    end
}
text Bought1Castelia{
    "{PLAYER} bought a Casteliacone!"
}
text Put1CasteliaInPocket{
    "{PLAYER} put away the Casteliacone\n"
    "in the Items pocket."
}
script Buy4Castelia{
    hidemoneybox
    checkitemspace(ITEM_CASTELIACONE, 4)
    if(var(VAR_RESULT) == FALSE){
        msgbox(NotRoomForCastelias, MSGBOX_NPC)
        end
    }
    checkmoney(1050)
    if(var(VAR_RESULT) == FALSE){
        msgbox(NotMoneyForCastelias, MSGBOX_NPC)
        end
    }
    playfanfare(MUS_OBTAIN_ITEM)
    message(Bought4Castelia)
	waitfanfare
	msgbox(Put4CasteliaInPocket, MSGBOX_DEFAULT)
    removemoney(1050)
    additem(ITEM_CASTELIACONE, 4)
    addvar(VAR_CASTELIACONES_BOUGHT, 4)
    waitmessage
    closemessage
    msgbox("Please come again!", MSGBOX_NPC)
    release
    end
}
text Bought4Castelia{
    "{PLAYER} bought a pack of 4\n"
    "Casteliacones!"
}
text Put4CasteliaInPocket{
    "{PLAYER} put away the Casteliacones\n"
    "in the Items pocket."
}
script CancelBuyingCastelia{
    msgbox(Shoreslate_CasteliaVendor_ThatsShame, MSGBOX_NPC)
    hidemoneybox
    end
}
text Shoreslate_CasteliaVendor_OutOfCones{
    "I'm sorry, but we have run out of\n"
    "Casteliacones for the day.\p"
    "We should have a new shipment\n"
    "coming in tomorrow."
}
text Shoreslate_CasteliaVendor_ThatsShame{
    "That's a shame…\p"
    "Well, let me know if you\n"
    "change your mind!"
}
text Shoreslate_CasteliaVendor_InterestInConesShort{
    "Would you like to buy some\n"
    "Casteliacones?\l"
    "One cone costs ¥350 but a pack\l"
    "of 4 costs only ¥1050!"
}
text Shoreslate_CasteliaVendor_InterestInCones{
    "I have recently acquired the coolest\n"
    "commodity all the way from Unova.\p"
    "That's right! It's everyone's favorite\n"
    "ice cream, the Casteliacone!\p"
    "Might I interest you in some\n"
    "Casteliacones?\l"
    "One cone costs ¥350 but a pack\l"
    "of 4 costs only ¥1050!"
}
text NotRoomForCastelias{
    "I'm sorry, but you don't have room\n"
    "to carry any more Casteliacones…"
}
text NotMoneyForCastelias{
    "I'm sorry, but you can't afford that…"
}

script Shoreslate_DevonExteriorTrigger{
    if(!defeated(TRAINER_SHORESLATE_ROCKET)){
        if(var(VAR_TEMP_6) == 1){
            lockall
            delay(5)
            turnobject(1, DIR_SOUTH)
            delay(5)
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
            msgbox(ShoreSlate_YouDeaf, MSGBOX_DEFAULT)
            waitmessage
            closemessage
            trainerbattle_no_intro(TRAINER_SHORESLATE_ROCKET, ShoreSlateRocketDefeat)
            release
            end
        }
        lockall
        delay(5)
        turnobject(1, DIR_SOUTH)
        delay(5)
        turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
        msgbox(ShoreSlate_NotGettingThrough, MSGBOX_DEFAULT)
        waitmessage
        closemessage
        setvar(VAR_TEMP_6, 1)
        applymovement(OBJ_EVENT_ID_PLAYER, MoveRight1)
        waitmovement(0)
        delay(5)
        release
        end
    }
    else{
        release
        end
    }
}

movement MoveRight1{
    walk_right
}

text ShoreSlate_NotGettingThrough{
    "Back off, pal.\n"
    "Nobody's going through here.\l"
    "This is your last and only warning!"
}

script Shoreslate_RocketM{
    if(!defeated(TRAINER_SHORESLATE_ROCKET)){
        msgbox(ShoreSlate_RocketStop, MSGBOX_NPC)
    }
    else{
        msgbox(ShoreSlate_RocketWontStop, MSGBOX_NPC)
    }
    end
}

text ShoreSlate_RocketStop{
    "The offices are closed. By the boss's\n"
    "orders, nobody is allowed to enter."
}
text ShoreSlate_RocketWontStop{
    "Guh…\n"
    "How could this happen…"
}
text ShoreSlate_YouDeaf{
    "I warned you!"
}
text ShoreSlateRocketDefeat{
    "What do I do…\p"
    "The boss is definitely going\n"
    "to be mad…"
}
